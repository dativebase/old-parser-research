################################################################################
# A Phonology of Blackfoot, as an FST, based on Frantz (1991)
################################################################################
#
# This file is a foma regular expression rewrite rule script that can be used
# to generate a FST that implements the phonology of Blackfoot, as described in
# Frantz (1991), i.e., the Blackfoot Grammar. In short, it implements the 
# phonological rules of Appendix B (Frantz 1991, pp. 152--155), the lexical
# phonological transformations of Chapter 15: Allomorphy (Frantz 1991, pp.
# 78--83), and other lexical transformations described in that text.
#
# Some things to note.
#
# 1. This phonology assumes that lexical items have the representations given
# in Frantz & Russell (1995), i.e., the Blackfoot Dictionary of Stems, Roots,
# and Affixes. Specifically, breaking /I/ is not used, nor are the
# non-permanent consonants indicated by means of capital <N>, <M>, or <S>.
# Straying from the dictionary, this phonology assumes that the period "." is
# NOT used to indicate underspecified semivowels at the ends of certain
# suffixes (cf. Frantz (1991, pp. 82--83). In short, it expects that only the
# characters ptkmnsywh'aioáíó are used. This means that some rules (i.e.,
# breaking, s-insertion, and neutralization) are modified or not implemented.
# Of course, a phonology (that perhaps performs better) could be built (maybe
# using this one as a starting point) that assumes a different lexicon; however,
# it seemed practical, for present purposes, to keep the lexicon as it is
# represented in the Dictionary and to encode lexical exceptions and
# allomorphy stuff in the phonology.
#
# 2. The phonology herein does not attempt to predict default (i.e.,
# non-lexical) prominence. This is an area that needs (and is currently
# receiving) more research. A revised version of this phonology will attempt
# to predict default prominence location, but that feat requires information
# about morphological domains (i.e., verbal prominence differs from nominal)
# and therefore requires a richer morphological representation as input to
# the phonology component during generation.
#
# 3. Frantz (1991) does not provide an explicit ordering the rules provided. 
# The FST named 'phonology' is an ordered list of composed FSTs, each of which
# implements a phonological rule. This ordered composition is functionally
# equivalent to an ordering of rewrite rules. It is an ordering that accords
# with Frantz (1991, p. 155)'s ordering generalization and which gets the data
# right. See the "Tests" section below for a list of UR/SR pairs that this
# phonology correctly encodes.
#
# 4. This phonology MASSIVELY OVERGENERATES. This means that during generation
# it will produce orthographic (surface) representations that include "the
# correct" one(s) but will also produce some incorrect ones. This means that
# it may not be ideal for use in producing a list of all possible Blackfoot
# words. When parsing, there will be even more ambiguity. However, this should
# not be an issue since the phonology should be composed with a morphology FST
# which encodes the lexicon of Frantz & Russell (1995) and the morphotactic
# possibilities of the language (cf. Beesley and Karttunen (2003)). Once such
# a morphology is in place, the entire morphophonology FST should produce one
# (or zero or a handful, but not a large or infinite set) of possible parses 
# for any given orthographic transcription input.
#
#
# How to use this file
################################################################################
#
# 1. Get foma: https://code.google.com/p/foma/ 
# 2. start foma at the command line terminal and run the following commands:
# 
#    foma[0]: source /path/to/this/file
#    foma[0]: regex phonology ;
#
# 3. Generate an orthographic representation of /nit-ihpiyi/ (should return
#    <nitsspiyi>, and possibly others):
#
#    foma[1]: down nit-ihpiyi
#
# 4. Parse an orthographically transcribed word:
#
#    foma[1]: up nitsspiyi
#
#
# How to understand this file
################################################################################
#
# - This is a foma FST script. See https://code.google.com/p/foma/ for details.
#   See also Beesley and Karttunen (2003).
#
# - "A -> B || C _ D" means "rewrite A as B only when it occurs between C and D"
#
# - "(->)" is optional rewrite
#
# - A, B, C, and D in the above rule can all be regular expressions. Note that
#   the foma parser treats regular expressions a little differently than what
#   you may be used to (especially wrt whitespace).
#
# - "define name expression ;" assigns the FSM/FST generated by "expression" to
#   "name".
#
# - Special characters (e.g., "-") need to be enclosed in quotes (or preceded
#   by %).
#
# - ".o." denotes the composition operation
#
# - "[..]" denotes the empty symbol on the lefthand side of a rewrite rule,
#   while "0" denotes that symbol on the righthand side. I.e., to elide "y"
#   you write y -> 0, but to epenthesize "y" you write [..] -> y. (Using "0"
#   on the lefthand side will result in 1 or more insertions---usually not
#   what you want.)
#
#
#
# TODOs
################################################################################
#
# NOTE: variable-length vowel allomorphy should take care of the vowel shortening
# in the second-to-last vowel of nitssiksipawa.
#
# ssikópii has no lexical prominence in its dictionary entry. If this is accurate,
# then the morphological prominence rules must somehow account for it.
#
# ikáá is not listed in Frantz & Russell 1995 with lexical prominence ...
#
# The word kitsí'powataawaawa is actually transcribed as kitsí'powatawaawa in 
# Frantz (1991). However, this requires shortening of the variable-length a:
# direct suffix ... So I have changed the orthographic representation ...
# The following exhibits the same issue: nit-ssksino-aa-wa -> nitssksinoawa.
# Actually, the transcription is predicted by the phonology. It happens because
# vowel-shortening applies before desyllabification. The variable-length thing
# is irrelevant here.
#
# In the test nit-Ioyi -> nitsoyi, the result, according to the grammar, has
# prominence in syllable 1: nítsoyi. However, the current phonology cannot account
# for this. Also, how do we account for the behaviour of ooyi/Ioyi? I.e., why
# is it áóyi when á- is prefixed and ííyi in a past perfective meaning?
#
# The pii-hsini -> pissini test assumes there is a verb with /pii/ as its shape.
# However, this is not the case. This is the 0~i alternation thingy ...
#
# test nit-waanii -> nitaanii The result should be nitáánii but there are no default
# prominence rules ...
#
# ``The pronoun in these examples (e.g., ápao'takiyináyi) is evidently áyi,
# and if the segment preceding this pronoun is a vowel, that vowel is
# deleted'' (Frantz 1991, p. 48). This means we need a special áyi rule ...
#
#
# The final a of -hpinnaana and -o'pa is a predictable addition after an otherwise
# word-final consonant, rather than part of these morphemes per se. ... (Frantz 1991, p. 22, fn. 17)
#
# A sequence like y-ihs will become y'ihs (cf. Frantz (1991, p. 18)) and then
# y'ss (presibilation). This explains data like ihpiyi-hsin-yi -> ihpiy'ssini 
# It almost explains á'-isttohkohpi'yi-hsi -> áíisttohkohpiy'ssi but not
# quite since áíisttohkohpi'y'ssi is predicted ... The supplemental material on
# p. 6 of the grammar discusses something similar wrt <o'yss> sequences.
#
# In order for á'-o'too-yiniki -> áó'tooyiniki to work, we need to assume
# that 2sg.subj is yiniki, not iniki. Are there undesirable consequences that
# arise from assuming this? ...
#
# Non-permanent consonants are not indicated in the dictionary: atsikíN-istsi -> atsikíístsi
#
#
#
# Tests
################################################################################
#
# A line that begins with "#test" signifies a test.  After "#test" there should
# be a string of characters followed by "->" followed by another string of
# characters.  The first string is the underlying representation and the second
# is the anticipated surface representation. This convention facilitates creating
# a "test-driven" phonology. That is, you create the phonology, run the tests,
# and then alter the phonology, and keep repeating until all tests pass. Of 
# course, this requires a script which makes it easy to run the tests. The Online
# Linguistic Database (http://www.onlinelinguisticdatabase.org) implements such
# functionality.
#
# This phonology passes all of the tests listed below. These are all of the
# examples listed in Appendix B of the grammar and a representative subset of
# the examples from Chapter 15: Allomorphy. Some of the tests/examples have been
# modified to be more morphologically accurate, or to remove "I", "N", etc., or
# to neutralize accent/prominence effects that the phonology does not attempt to
# account for. Even so, there are still a lot of UR/SR pairs in the Grammar and
# Dictionary that this phonology cannot account for, but I leave that for
# future work.


# Gold Standard Tests
#-------------------------------------------------------------------------------
#
#
#
#test miistak-istsi -> miistakiists
#test nit-iik-isttsi-ikinaki -> nitsiksisttsikinaki
#test aak-waakaohtoo-m-wa -> aakakaohtooma
#test yiis-aaki -> yiisaki
#test i'pii-ika-iisi-wa -> i'pikaasiwa
#test n-iksisst -> niksisst
#test a-ssi-aaki -> aissaakii
#test iik-waakomimm-yii-wa -> ikakomimmiiwa
#test ikkiaaki-a'tsis -> ikkiaakia'tsis
#test iik-a-sska'-ooyi -> iikaisskooyi
#test si'kat-yii-wa -> isi'katsi
#test nit-aak-otaahsowaa -> nitakotaahso
#test imitaa -> imitaa
#test nit-ipahtsa'pssi -> nitsipahtsa'pssi
#test ii-ohp-miistap-okska'si -> iihpistapookskasi
#test ipo-oo -> ipowaoo
#test nit-ssik-mo'kakin-aa -> nitssikoo'kakina
#test nit-a-ssp-ihka'si -> nitaisspihka'si
#test kit-iik-sst -> kitsiihkst
#test sina-aki-a'tsis -> sinaakia'tsis
#test naato-naapi-ikoan -> naatoyaapiikoan
#test ko'ko-yi -> koo'koyi
#test ni'tokskaa -> nii'tookska
#test it-a-si'tsii-wa -> itaiaahkitsiw
#test ot-itan -> otan
#test n-i's-aawa -> ni'saawa
#test nit-a-ssi-aaki -> nitaissaaki
#test it-o'kaas-atw-i-m -> itokasatom
#test inihki -> iinihkii
#test ipak-ika-yii -> ipaksikay
#test ann-yi-hkayi -> aniskayi
#test nit-ohp-ihka'si -> nitohpihka'si
#test nit-aohkii-im-yi -> nitaohkiimi
#test nit-i'poyi-ipitsi -> nitsi'poyiipitsi
#test nit-sskani'taam -> nitsskani'taam
#test ii-ohpok-ino-yi-m-yii -> iihpokinoyimiwa
#test aak-ipsst-a'p-a-ssapi-wa -> aaksipssta'paissapiwa
#test ikkahs-i'taki -> ikkahsi'taki
#test waakan-yii -> aakani
#test issk -> issk
#test kit-ohpok-okska'si-m-ok-wa -> kitohpokokska'simoka
#test stam-aanist-a'p-si-wa'si -> stammaanista'psiiwa'si
#test omahk-a-ssp-iksi-mm -> iimahkaisspiksim
#test nit-iksimatsi'tomo-ok-wa -> nitsiksimatsi'tomooka
#test ikksk-si -> ikkskssi
#test naapioyis -> naapioyis
#test iik-ohpok-im-yi -> iikohpokimii
#test nit-a-ihpiyi-hpinnaan -> nitaihpi'ihpinnan
#test i'pistotsi-m-wa -> i'pistotsim'a
#test nit-it-a-opii -> nitsitaopii
#test nit-a-ayak-inihki-hpinnaan -> nitayaakain'ihkihpinnan
#test iik-waanat-inaattsi-yi-aawa -> iikaanatsinaattsiiyaawa
#test n-mo'tokaan-yi -> no'tokaani
#test nit-iik-a-iksist-ihkiitat-aa -> nikaiksisskiitaata
#test iik-i'taam-si-yi-aawa -> iiksi'taamssiiyaawa
#test ninaa-wa'si -> iinawa'sii
#test iinii -> iinii
#test n-ohko-iksi -> nohkoiksi
#test nit-imitaa-im-hk-o-aa -> nitsiimitam'sskoaa
#test akaa-onnat-ohsi-wa -> akaonnatohsiiwa
#test m-aahk-iksist-ookataki-hsi -> maahksiksistookatakssi
#test mai'stoo-iksi -> mai'stooiks
#test nit-yisskoyipist-ok-wa -> nitsisskoyipistoka
#test ohpommaa-wa -> iihpommaawa
#test iiht-ihpiyi-attsi-wa -> iihtsspiyattsiwa
#test piksiiksiinaa -> piksiiksina
#test it-a-iso-opii-o'p-yi -> iitaisoopao'pi
#test oka'p-i'poyi -> oka'pi'poyi
#test nit-a-waanii-ok-wa -> nitaawaanika
#test maohk-inaa-mm -> amohksinaamm
#test sik-im-yi-ota's-yi -> sikimiota'si
#test oto-ikskimaa -> iitooiikskimaa
#test iik-mattsi-aakii -> iikattsaakii
#test nit-inaani -> nitsinaani
#test sapikamaa-wa -> isapikamaawa
#test iksist-yi -> iksistsii
#test mattsi-aakii -> mattsaakii
#test apoyi-inaattsi-istsi -> apoyiinaattsiistsi
#test i'ksisako-yi -> i'ksisakoyi
#test n-itakkaa-wa -> nitakkawa
#test nit-ninaa-ihka'si -> nitsinaihka'si
#test nit-wa'tsi-aa -> nita'tsa
#test aanist-inaa -> aanistsinaa
#test a-okstsimaa-a'tsis -> akstsima'tsis
#test ipo't-ika-aaki-wa -> iipo'tsikaakiwa
#test aahkioohsa'tsis -> aahkioohsa'tsis
#test ni't-o'too -> ni'to'too
#test omahk-yi-aawa -> omahkoyaawa
#test ni't-ikopoto -> nii'tsikopoto
#test nit-aak-itap-omaahkaa -> nitaaksiitapomaahka
#test ni-ikopoto -> niikopoto
#test kit-ohpok-waahkaniaaki-m-aawa -> kitohpokaahkaniaakiimaawa
#test nit-aak-kipp-omatap-oo -> nitaakippomatapo
#test n-ota's-iksi -> noota'siiks
#test nit-iik-i's-imm-ohsi -> nitsiksi'simmohsi
#test stam-ssk-waahkayi-a'p-omaahkaa -> stamsskahkia'pomaahkaa
#test nit-sstsimaahkat-aa-wa -> nitsstsimaahkataawa
#test nit-soyoohpi'yi -> nitsoyoohpi
#test otsistapi't-i -> otsistapi'tsi
#test iik-ok-a'p-istawat-yii -> iikooka'pistawatsii
#test am-o -> amo
#test nit-ipak-ika-ok-wa -> nitsipaksikaoka
#test aak-waamohs-ika-hkaa-wa -> aakamohsikahkaawa
#test nit-maat-oko's-hpa -> nimaatokoo'sspa
#test ikkam-si-atw-i -> ikkamssatoo
#test k-apssi -> koohpsii
#test yiimaapitsi-wa -> iimaapitsiwa
#test ann-wa-hka -> annaahk
#test aak-ipak-sski-yii-wa -> aaksipaksskiyiiwa
#test iihko-imitaa-im-yi -> iihkoomitami
#test it-a-iso-ooyi-o'p-yi -> iitaisooyo'pi
#test nit-yo'kaa-hp-yi -> nitso'kaahpi
#test aak-a-waawattoo-wa -> aakawaawattoowa
#test wa'sok-hko -> wa'sokssko
#test ohko-yi -> ohkoy
#test waanii-hsin -> anissin
#test matonni -> matonnii
#test nit-aak-ohpommaa -> nitaakohpomma
#test ni't-ohpokinnakihsin-yi -> ni'tohpokinnakihsini
#test oyi't-o -> oyi'to
#test nit-o't-o-ok-wa -> nito'tooka
#test ipsstsik-aakii-wa -> ipsstsikaakiiwa
#test k-itan-nnoon-wa -> kitaninnoona
#test iik-waakayi-mm-yi-aawa -> iikakayimmiaawa
#test paataak-istsi -> paataakistsi
#test sistsikoo-t -> sistsikoot
#test ikkahs-istot-o -> ikkahsistoto
#test waamsskaap-oht-yi -> aamsskaapoohtsi
#test oot-o -> ooto
#test i'poyi-yi-aawa-hsi -> i'poyiiya'si
#test ssi-ika-aa'si -> issiikaasi
#test asoka'siistsi -> asooka'simiists
#test ihpiyi-t -> ihpiyit
#test istsi'tsaana'pssi-t -> istsi'tsaana'pssit
#test iiyik-sopo-yi -> iiyiksopoyi
#test iik-i'taam-I-yi-aawa -> iiki'taamssiyaawa
#test ipon-ota's-imm -> iponota'simm
#test nit-ikskimaa -> nitsikskim
#test i't-mohksiston-yi -> i'tsoohksistoni
#test aaattsistaa -> aattsistaa
#test nit-oohkotok-im-yi -> nitoohkotokimi
#test ot-piksiiksiinaa -> otpiksiiksiinaa
#test ohpok-ooyi-m-yii -> ohpoksooyiimii
#test iik-ikkihkini-inaa-mm -> iiksikkihkiniinaamm
#test onnikis -> onnikis
#test sok-a'p-yi -> soka'pii
#test maiipssim-i -> maiipssii
#test sattsik-sski-o't-o -> sattsiksskio'to
#test nit-ipak-ika-aa -> nitsipaksikaa
#test sstonnat-imitaa-sski -> isstonnatomitaisski
#test om-yi -> omyi
#test nit-oto-iso-ooyi-hkaa -> nitsiitosoyo'sska
#test mohpiikin-istsi -> ohpiikiistsi
#test maat-aak-ohkott-a'pohpattski-m-waatsiksi -> maatakohkotta'pohpattskimaatsiksi
#test ot-iksistsikomssin-yi -> otsiksistsikomayi
#test waatsimma'pssi-wa -> aatsimma'pssiwa
#test iik-sik-aapin-I -> iiksikapini
#test nit-sspitaa -> nitsspitaa
#test waanist-wa -> nista
#test na-inihki-attsi-aa-wa -> nainihkiattsaawa
#test n-aaahs-wa -> naahsa
#test ii-ohp-o'too -> iihpo'too
#test omahk-moyis -> omahkoyis
#test a'p-a-istot-aaki-wa -> a'paistotakiwa
#test nit-iik-a-ok-inaa -> nitsikaoksin
#test nit-ipakk-hk-aa -> nitsipakksskaa
#test am-o-iksi -> amoksi
#test niistsiipisskaan-yi -> niistsiipisskaani
#test ayaak-waasai'ni-wa -> ayaakasai'niwa
#test n-insst -> ninnst
#test nit-omikso't-i-hp-wa -> nitsiimikso'tsii'pa
#test nit-ohp-oko's -> nitohpoko's
#test iit-a-opii -> iitaopii
#test nit-ohkot-aa-hp-yi -> nitohkotahpi
#test aak-oto-i'poyi -> aakotoi'poyi
#test ihpiyi -> ihpiyi
#test n-aanist-waakomimm-yi-hp-yi -> naanistakomimmihpi
#test a-sstahpik-ohpok-ihpiyi-m-yii-wa -> aisstahpikohpokihpiyimiwa
#test si'kaan -> si'kaan
#test pisatssaisski -> pisatssaisski
#test iksistto-atw -> iksisttowat
#test i'nak-oht-yi -> inakoohtsi
#test aak-ohpok-iitsskaa-o'pa -> aakohpokiitssko'pa
#test ko's-istsi -> ohkoo'sists
#test yaamaahki-aaki -> yaamaahkiaaki
#test mikksk-napayin -> mikskapayin
#test kakkoo-wa -> kakkoowa
#test ponoka -> ponoka
#test i'ni -> i'ni
#test ot-imitaa-im-yi -> otimiitaam
#test aapinakos-yi -> apinakosi
#test a-okska'si -> aokska'si
#test itsi-a'p-yi -> itsowa'pii
#test nit-iitsskaa -> nitsiitsskaa
#test iit-waatsimoyihkaa -> iitawaatsimoyihkaa
#test a-ssi-iststaki -> aissiiststaakii
#test issk-wa -> isska
#test a-sstsim-ohpok-ihpiyi-m-yii-wa -> aisstsimohpokihpiyimiwa
#test saahkomaapi-iksi -> saahkomaapiksi
#test tsa -> tsaniita'pii
#test kit-sik-ika-ikoan -> kitsiksikai'koowan
#test nit-otahpiaaki -> nitotahpiaaki
#test om-istsi -> omistsi
#test apoyi-ihkin-yi -> waapoyiihkini
#test imitaa-iksi -> imitaaiks
#test it-opii-t -> istopiit
#test stam-ohkott-sspomoo -> stam'ohkotsspomoo
#test a-iksiksiimohki-ohtako -> aiksiksiimohtako
#test ota's-imm -> ota'simm
#test iikootsip-inaattsi -> iikootsipiinattsi
#test imitaa-wa -> imitaawa
#test ksis-ist-mm -> iksisistsim
#test aak-aisskahs-a-waasai'ni-wa -> aakaisskahsawaasai'niwa
#test ni'tommo-istsi -> ni'tommoistsi
#test anniihk -> anniihk
#test isttsik-yi -> isttsikii
#test kaak-oht-ooto-ssamm-yii-wa-ayi -> kaakohtootaissammiiwayi
#test ksikk-a'p-istot-aaki -> iksikka'pistotaki
#test pookaa-iksi -> pookaiksi
#test iik-isttsi -> iiksisttsii
#test pakoyittsi -> pakoyittsi
#test kit-aak-sa'kssoyi -> kitaaksaaksoy
#test sipi-kakato'si -> sipikakato'si
#test ipsst-ip-ohtoo -> ipsstsipohtoo
#test iit-a'p-a-okska'si-wa -> iita'paoksk'asiwa
#test n-itan-nnaan-iksi -> nitaninnaaniksi
#test ponn-atw -> ohponnat
#test it-ssok-ihka'si -> itsssokihka'si
#test iik-yaahs-a'p-yi -> iikaahsa'pii
#test iik-omahk-inaattsi -> iikomahksinattsi
#test a-ikkihkini-inaa-mm-yi-aawa -> aikkihkiniinaammiyaawa
#test apasstaamiinaamm -> apasstaminam
#test ihkiitaa-wa -> iihkiita
#test iik-inikk-inaa-mm -> iikinikksinaamm
#test istot-imm -> istotsimm
#test a-sstsim-ohpok-ooyi-m-yii-wa -> aisstimohpokooyimyii
#test iiht-a-ikahk-iststaki-o'pa-iksi -> iihtaikahksiststakio'piksi
#test oto-ohkohtaa -> otohkohtaa
#test nit-ksikk-ap-istot-o-aa -> nitsiiksikka'pistotowaa
#test iit-ino-yii -> iitsinoyii
#test ii-ohpok-istsitsii-m-yii -> iihpokistssoyimi
#test moyis -> moyis
#test nit-iik-aakii-ipitsi -> nitsiikaakiipitsi
#test ot-isskan -> oskanni
#test atapiim-wa -> atapiima
#
#
# Reusable Regexes
################################################################################
#
define phonemes [ p | t | k | m | n | s | w | y | h | "'" | a | i | o | á | í | ó ] ;
define vowels [ a | i | o | á | í | ó ] ;
define accentedVowels [ á | í | ó ] ;
define consonants [ p | t | k | m | n | s | w | y ] ;
define obstruents [ p | t | k | m | n | s ] ;
define stops [ p | t | k | m | n ] ;
define plosives [ p | t | k ] ;
define glides [ w | y ] ;



# Allomorphic Transformations (cf. Frantz 1991, pp. 78--83)
################################################################################

# Many of the transformations listed here are quite ad hoc and contain lots of
# tedious environments based on lexical conditions. Putting so much lexical
# information into the phonology is perhaps not elegant, but it works. In
# fact, to make this phonology better, more lexical detail should be included
# here.
#
# A. Initial Vowel Elision in Imperatives: ipii-t -> piit (Frantz 1991, pp. 78--79)
# Note that forms like ihpiyi-t are ihpiyit, hence the consonants regex below.
# Note that this elision might be more general; i.e., it might occur with
# nominalizations too.
# Note that the first FST in the composition below transforms i(i)t- to ist- in
# imperatives (cf. Frantz 1991, p. 96, fn. 120). Note also that the rather
# complex conditions on i-elision (i.e., the second compositum) are needed in
# order to allow for i(i)t- -> ist- to occur without it becoming st-.
define imperativeInitialVowelElision [
    [ [..] -> s || .#. (i) i _
        t "-" [ phonemes | "-" ]+ "-" t .#. ] .o.
    [ i -> 0 || .#. _
        [ consonants - s ] [ [ phonemes | "-" ] - s ]
        phonemes [ phonemes | "-" ]* "-" t .#. ] .o.
    [ o h -> 0 || .#. _
        consonants [ phonemes | "-" ]+ "-" t .#. ] ] ;

# B. Stop-stop Epenthesis: sik-kiááyo -> sikohkiááyo (Frantz 1991, pp. 78--79)
define stopStopEpenthesis [
    [..] -> [ i | o h ] || plosives "-" _ [ plosives | n á á m a y i ] ] ;

# C. Coda-fication: sik-ponoká -> siksinnoka (Frantz 1991, p. 79)
define codafication [
    p o n -> i n n || stops "-" _ o k á [ "-" | .#. ] .o.
    k i i p -> i p p || stops "-" _ ó [ "-" | .#. ] .o.
    k i p -> i p p || stops "-" _ i t a [ "-" | .#. ] ] ;

# D. Nasal Loss: omahk-natáyo-wa -> omahkatayowa (Frantz 1991, p. 80)
define nasalLoss [ m | n ] -> 0 || consonants "-" _ ;

# E. Word-initial nasals
# Some verbs that appear to start with vowels, actually begin with a nasal
# word-initially, or, more accurately, in imperative or nominalization
# constructions. There seems to be some quite ad hoc lexical/phonological
# alternations here that are just begging for a more unified explanataion.
# This type of data might be evidence that the third person past tense forms
# do actually involve prefixation because such an analysis would allow us to
# group together the imperatives and nominalization constructions as truly
# having the stem at the left edge. Note: there are several other verbs that
# begin with a nasal (m or n) when in word-initial position. I don't have
# time to write them all into a rule right now. Searching the OLD for ^[mn].*t!$
# as a regexp in the orthographic transcription and excluding results that
# match the regexp (^| )(máát|miin) in the orthographic transcription will
# get them.
define impNomSfx "-" [ t | n | h s i n ] [ "-" | .#. ] ;
define nasalInitialVerbs [
    [ y a (a) -> n a || .#. _ m a a [ h k i a a k i | k i o k o "'" s i ] impNomSfx ] .o.
    [ y -> n         || .#. _ á á p i i "'" p o y i impNomSfx ] .o.
    [ [..] (->) n    || .#. _ [ i n i h k i | i p a i t a p i i y i ] impNomSfx ] .o.
    [ [..] -> n      || .#. _ i i t s i "'" p o y i impNomSfx ] .o.
    [ [..] -> n i    || .#. _ i p [ ó í s o y a a w a n i | o w á ó o ] impNomSfx ] .o.
    [ i s -> n a a   || .#. _ t s i k a p ó ó y i n n i impNomSfx ] .o.
    [ i s -> n a a   || .#. _ t s i k a p o i s í n a i i impNomSfx ] .o.
    [ o -> m a       || .#. _ t o o m [ phonemes | "-" ]+ impNomSfx ] ] ;

# F. Word-initial nominal segments become o elsewhere (Frantz 1991, p. 80)
# Note: this is probably a more common phenomenon than is captured by this
# rule! I just don't have time to sort through all of the lexical items where
# this occurs.
define becomeO [
    [ i -> o   || plosives "-" _ m i t á á ] .o. 
    [ m a -> o || plosives "-" _ [ m í í | n i s t s í ] ] .o.
    [ i s (->) o || "-" _ t t o á n ] ] ;

# G. Word-initial verbal o becomes a elsewhere (Frantz 1991, p. 80)
# Optionally happens with okstaki and okaa (and maybe others ...)
define becomeA [ o (->) a || .#. _ k [ a a | s t a k i ] impNomSfx ] ;

# I. Variable-length vowels (Frantz 1991, pp. 80--81)
# Certain long vowels in certain morphemes are actually variable in length.
# These vowels are only long in certain environments, are variably long
# in one specific environment and are short elsewhere. Writing rewrite rules
# to account for this would be too time-consuming for me right now. Therefore
# I am going to create a rule that optionally shortens these vowels.
# The complex context in the following rules is meant to capture the fact that
# the first vowels in the following morphemes are variably long: wa:nii `say' (vai)
# a: `DIR', wa:wahkaa `play' (vai), wa:wayáki `hit' (vta), wa:yáki `hit' (vta),
# wa:sai'ni `cry' (vai), wa:hkayi `go home' (vai), sa:ksi `exit' (vai),
# a:nist `manner' (adt), wa:wakaa `walk' (vai), a:ki `vai-producing suffix' (fin),
# and wa:mi `be' (vai).
# Here is the generalization. Variable-length vowels are always long if any of
# the following are true: i) they are in the first syllable of a word, ii) they
# are preceded by a semivowel in the same morpheme that is not deleted by
# semivowel loss, or iii) they are preceded by i underlyingly, i.e., even if the
# i is deleted by i-Loss or i-Absorption. Variable-length vowels are variably
# long or short when preceded by one of the person prefixes nit-, kit- or ot-. In
# all other environments, these vowels are short. Note that many of the vowels
# from the agreement paradigms are like this (cf. around p. 149 of the Grammar).
# However, these seem to be really just portmanteau morphemes built upon the
# direct suffixes a: and o: ...
# WARNING: I think there is a much wider application of this rule than is specified
# here; i.e., there are many more morphemes that begin with variable-length vowels.
# THIS NEEDS WORK!
define variableLengthVowels [
    [ a a (->) a || [ .#. | "-" ] w _ [ n i i | n i s t | n i s t o o |
                                        w a h k a a |
                                        w a y á k i |
                                        y á k i |
                                        s a i "'" n i |
                                        h k a y i | 
                                        w a k a a |
                                        m i ] [ .#. | "-" ] ,
                    "-" _ "-" ,
                    [ .#. | "-" ] s _ k s i [ .#. | "-" ],
                    [ .#. | "-" ] _ [ n i s t | k i ] [ .#. | "-" ] ] .o.
    [ o o (->) o || "-" _ "-" ] .o.
    [ o (->) o o || "-" _ k ( [ o o | i ] ) "-" ] ] ;

# J. Non-permanent consonants (Frantz 1991, p. 81--82, 9)
# These consonants disappear before vowels. Since the dictionary does not
# employ the capitalization convention for these consonants, I posit an optional
# elision rule for morpheme-final /n/ and /s/ before vowels and a regular
# elision rule for morpheme-final /m/ in áto'ahsim `sock', máíipssim `belt',
# and asóka'sim `dress'.
# Note: the optionality-based implementation will cause yini-áyi to become
# correctly yin-áyi but also incorrectly yi-áyi #
# Note: the rule that is faithful to the N/M/S phonemes of the grammar is given
# below:
# define nonPermanentConsonants [
#     [ [ N | M | S ] -> 0 || _ "-" vowels ] .o.
#     [ N -> n , M -> m , S -> s || _ [ "-" | .#. ] ] ] ;
define nonPermanentConsonants [
    [ [ n | s ] (->) 0 || _ "-" vowels ] .o.
    [ m -> 0 || [
        á t o "'" a h s i |
        m á í i p s s i |
        a s ó k a "'" s i ] _
        "-" vowels ] ] ;

# K. 3mm (Frantz 1991, p. 82)
# Certain intransitive verbs have mm suffixed when a consonant-initial 
# morpheme follows. These verbs have been identified and are listed in
# the lexical phonological rule given below. Note that ikaissi has -mm
# suffixed optionally.
define 3mm [
    [ [..] -> m m || [ .#. | "-" ] [
        i k o ' p o |
        i k s i s á í i k i |
        i k s i s t o ' s i |
        i m i |
        i n á á m m |
        i n n o ' s i |
        i p a h k s i m i |
        p á p i s a |
        i p a p o |
        i p i i |
        i p o i n a o h k o y i |
        i p o n i n a |
        i s t t s í í k i n a k i |
        i t s i k s i n a |
        i t s a ' t a |
        i ' n á k s t s s i |
        i ' t s o y i |
        k s i k k s i n a a |
        m a o h k s i n a a |
        m i ' k s i n a a |
        n i i s i t o y i |
        n i ' t ó k s k a a |
        o h k o y i |
        o h k s s s a a n i i n a a |
        ó k o h k o y i |
        o k a ' p i h t s i m i |
        o k s i k i n a k i |
        o k s i n a |
        o m a h k s i |
        o t a i m i |
        o t s i |
        o ' k s t s i k s i n a a t t s i |
        o ' k s t s í k s i n a a |
        o ' t s s o p o k i i y i |
        s a a h k s s t s s i |
        s a t s i k o h t a a |
        s a y í n a k o y i |
        s s k í i n a a |
        s s k a a |
        s s t o ' s i |
        w a a k a y i |
        w a a p i s o y i i n a |
        y a a h s s i |
        y o o h s i n i i n a ] _ "-" [ w a | y i | y i n i ] [ "-" | .#. ] ] .o.
    [ [..] (->) m m || [ .#. | "-" ] i k a i s s i _ "-" [ w a | y i | y i n i ] [ "-" | .#. ] ] ] ;
        #y o o h s i n i i n a ] _ "-" [ consonants - t ] ] .o.
    #[ [..] (->) m m || [ .#. | "-" ] i k a i s s i _ "-" [ consonants - t ] ] ] ;


# L. Semivowel alternation (Frantz 1991, pp. 82--83)
# Note: the root oo `eat' arguably exhibits this too (cf. oo-i -> ooyi and
# oo-at -> oowat, Frantz 1991, p. 100, fn. 126)
define SVAltMorphs [
    i h t a |
    i h t s o o h k i t s i |
    i k a |
    i k k i a "'" |
    i k s i s s t o |
    i n s s t a |
    i s a k o o |
    i t s i |
    m a t s i |
    n a a t o |
    o h p i i |
    o h t o |
    s o p o |
    w a a h k o |
    w a a k o o "'" |
    w a a w o ] ;
define semivowelAlternation [
    [ [..] -> y || [ .#. | "-" ] SVAltMorphs _ "-" [ i | í ] ] .o.
    [ [..] -> w || [ .#. | "-" ] SVAltMorphs _ "-" [ o | ó | a | á ] ] ] ;

# M. Diphthongization
# This phenomenon involves stems that end in oo, ii or i and which change
# this final vowel to ao, ao, and a (respectively) before the 21 suffixes
# -o'p, -o's, and -o'k (Frantz 1991, p. 83). Note: I'm not sure how accent
# marking is affected by this rule or what exactly áóo is supposed to represent
# phonetically in itapáóo'pa (Frantz 1991, p. 83).
# Note: I think there are more stems that behave like this. I may need to
# make this into an optional rule in order to account for this ...
# Note 2: the <o> in the replacement is here made optional since sometimes this
# is transcribed as <aoo> and sometimes just <ao>. In fact, I am not sure what,
# exactly Frantz means by sequences like <aoo> and <aii>.
define 21Sfxs "-" o "'" [ p (a) | [ s | k ] (i) ] [ "-" | .#. ] ;
define diphthongization [
    [ o o -> a (o) || [ .#. | "-" ] [
        i h t s i s |
        k i "'" t s i i k s o w |
        i s t t s s |
        i t a p |
        o h t s s a p |
        o p a s |
        s i s t s i k |
        s s k |
        w a a w a a t t s i s o w |
        y i i s t a p ] _ 21Sfxs ,
        [ .#. | "-" ] _ 21Sfxs ] .o.
    [ ó o -> a (o) || [ .#. | "-" ] s o w _ 21Sfxs ] .o.
    [ i i -> a (o) || [ o | ó ] p _ 21Sfxs ] .o.
    [ i -> a (o)   || [ .#. | "-" ] [ i s t a w a "'" s | i p i k s ] _ 21Sfxs ] ] ;

# N. Initial Change -ay- (Frantz 1991, p. 36)
# A small subset of stems beginning with sV or ICV (where C and V stand for any
# consonant and vowel), replacement of this initial sequence by sayV or CayV,
# respectively. This is possible only in word-initial position. Initial change
# is also found in some imperative forms and a few nominalized verbs.
# Note: this is REALLY UGLY! There has to be some better way of capturing this ...
define initialChangeAY [
    [ i k -> k a y || .#. _ [
        i i |
        i p p |
        i "'" t a w a a t o o |
        i "'" t a y i s s k s i m m a a ] [ .#. | "-" ] ] .o.
    [ i k s -> k a y || .#. _ i s s t a "'" p s s i [ .#. | "-" ] ] .o.
    [ i p -> p a y || .#. _ i p o "'" t a m i a p i k s s a t t s i i y i [ .#. | "-" ] ] .o.
    [ i p (->) p a y || .#. _ i p o t t a a [ .#. | "-" ] ] .o.
    [ k s (->) k a y || .#. _ i k k [ .#. | "-" ] ] .o.
    [ s -> s a y || .#. _ [ a "'" k a p | a t t s i k s s k a a ] [ .#. | "-" ] ] .o.
    [ s (->) s a y || .#. _ o o t a a [ .#. | "-" ] ] .o.
    [ i k s í í -> k s í í y i || .#. _ k s k a a a t [ .#. | "-" ] ] .o.
    [ i k a -> k á á || .#. _ y i h t s i [ .#. | "-" ] ] .o.
    [ i k a -> [ k a a | i i k á ] || .#. _ y i n n i [ .#. | "-" ] ] ] ;

# N.a. Initial Change ii- (Frantz 1991, p. 36)
# This is the ii-prefixation/ change-left-edge-vowels-to-ii initial
# change that the Grammar discusses. Since almost all verbs begin with
# a vowel, a semivowel, or <s> (given the conventions of the dictionary) we
# can use this fact to reduce the massive overgeneralization (i.e., to non-verbs)
# that this rule would otherwise bring about. Still, it's not pretty ...
# WARNING TODO !!!: VERB STEMS THAT ARE FUTURE OR IMPERFECTIVE SHOULD NOT UNDERGO INITIAL CHANGE LIKE THIS! THIS RULE CURRENTLY INCORRECTLY DOES THAT
define initialChangeII [
    [ [..] (->) i i    || [ .#. | .#. [ n i t | k i t | o t ] "-" ] _ [ s | w | y ] ] .o.
    [ vowels+ (->) i i || [ .#. | .#. [ n i t | k i t | o t ] "-" ] _ [ consonants | "'" | h ] ] ] ;

# O. yi-Loss
# Certain verb stems lose a final /yi/ when third person suffixes are added
# (Frantz & Russell 1995, p. xvii). Note: I assume that this only happens
# after -wa, -yi, and -yini, although maybe the third person suffixes of
# different moods have this effect also. Note also that this rule may be
# optional or speaker-conditioned (cf. the parenthesized "yi"s in the
# dictionary).
define yiLoss [
    y i -> 0 || [ .#. | "-"] [
        i k k a n i k s o o h p i "'" |
        i n n i s i "'" |
        i p a i t a p i i |
        i t á "'" p a o h p i "'" |
        i t a p i i |
        i "'" t o m i k á ó h p a i "'" p i |
        o h p i "'" |
        o "'" t s s o p o k i i |
        s a a p o h p i "'" |
        s o y o o h p i "'" |
        s s k s s p o h p i "'" |
        w á á p a t s i i "'" |
        w a a t a p o k a "'" ]
        _
        [ y i | w a | y i n i ]
        [ .#. | "-" ] ] ;

# P. áyi DTP vowel elision
# According to Frantz (1991, p. 48) vowels are deleted before the DTP suffixes -áyi, -aistsi, and -aiksi
define DTPVowelElision [ vowels -> 0 ||
    _ "-" [ á y i | a i [ s t s i | k s i ] ] [ .#. | "-" ] ] ;

# Q. Irregular (lexical) breaking
# Certain verbs break (i.e., assibilate) preceding /t/ and /k/ even though
# they do not ostensible begin with a high front vowel. Examples are ooyi 'eat'
# (and its cousins), yo'kaa 'sleep', and yáapi 'see'. The first two of these
# are sometimes transcribed in the grammar as Ioyi and Io'kaa, respectively. Here I create
# an ad hoc, lexically sensitive rule # to deal with these. Of course, there
# are probably other verbs that behave # in this way.
# N.b. I replace the delimiter here to prevent the subsequent application of
# s-connection a which would undesirably lengthen the <s>.
define irregularBreaking [
    "-" -> s || [ t | k ] _ [
        o o [ y i | w a t ( o o ) ] |
        y o ' k a a ] [ .#. | "-" ] ] ; 

define irregularBreaking [
    [ o -> i || [ t | k ] "-" _ o [ y i | w a t ( o o ) ] [ .#. | "-" ] ] .o.
    [ y -> i || [ t | k ] "-" _ [ o ' k a a | á a p i ] [ .#. | "-" ] ] ] ;

# R. Inverse Clipping
# The <o> of the inverse suffixes (-ok, -okoo, -oki) disappears before verbs
# that end in <t> (Frantz 1991, pp. 57--58).
# Note: the lengthening of this same <o> in different contexts is handled by
# the variable-length vowels rule.
define inverseClipping o -> 0 || t "-" _ k ( [ o o | i ] ) "-" ;


# Phonological Rules (cf. Frantz 1991, pp. 152--155)
################################################################################

# 1.    C1-C2     ->     C2C2
# Gemination Original
# Note that this rule is actually quite contrived since it requires that the
# adjacent plosives be MADE adjacent by a previously applying rule. If they
# were underlyingly adjacent, then the so-called ``stop-stop epenthesis'' rule
# would insert /oh/ or /i/ between them. Note: geminationGeneral is not being
# used.
define geminationGeneral [
    [ plosives "-" -> p || _ p ] .o.
    [ plosives "-" -> t || _ t ] .o.
    [ plosives "-" -> k || _ k ] ] ;
define gemination [
    [ a n i s t -> a n i k || _ "-" k ] .o.
    [ t -> k || _ "-" k ] ] ;

# 2.    It    ->    Ist
# s-Insertion (assumes that "breaking I" is a phoneme)
# Note: since the dictionary does not use /I/, this rule is not being used.
# Instead it is assumed that words like waanIt 'say to' are represented
# phonemically as /waanist/ and there are special lexical rules to deal with
# /waanist-ok/ -> [waanikk], etc. (see above).
define sInsertion [..] -> s || I _ t ;

# 3.    C-s -> Css, V(')-s -> V(')-is
# s-Connection
# condition on s-Connection "B": the 's' is not part of a suffix;
# present implementation: transformation is optional.
define sConnection [
    [ "-" -> s || stops _ s ] .o.
    [ [..] (->) i || vowels ("'") "-" _ s ] ] ;

# 4.    o-a    ->    aa
# o-Replacement
# note: for some speakers the o is deleted
# condition: where 'a' is not part of a suffix
# present implementation: rule is optional
define oReplacement [
    [ o (->) [ a | 0 ] , ó (->) [ á | 0 ] || _ "-" a ] .o.
    [ [ o | ó ] (->) [ á | 0 ] || _ "-" á ] ] ;

# 5.    w-i(i)    ->    o
# Coalescence
define coalescence [
    w "-" i (i) -> o ,
    w "-" í (í) -> ó ||
    _ [ consonants | h | "'" ] ] ;

# 5b.   i -> o / C_w{a,o}
# Coalescence 2 (Frantz 1991, p. 70, fn. 79)
# TODO: implement this rule! Test based on examples on p. 69 of grammar

# 6.    k-I    ->    ksi
# Breaking
# Note: the commented-out rule is what it should be. However, since
# the lexical items of the dictionary lack /I/, I use an optional 
# breaking transformation with /i/.
#define breaking "-" -> s || k _ I ;
define breaking "-" (->) s || k _ [ i | í ] ;

# 7.    I -> i
# Neutralization
# Again, since the dictionary doesn't use /I/, this rule is superfluous.
define neutralization I -> i ;

# 8. Desyllabification
define desyllabification [
    [ "-" i -> y || vowels _ [ a | á | o | ó ] ] .o.
    [ "-" o -> w || vowels _ [ a | á | i | í | ó ] ] ] ;

# 9.    #G    ->    0
# Semivowel Drop
define semivowelDrop glides -> 0 || .#. _ ;

# 10.    V1V1-V    ->    V1V
# Vowel Shortening
define vowelShortening [
    [ [ a | á ] -> 0 || [ a | á ] _ "-" vowels ] .o.
    [ [ i | í ] -> 0 || [ i | í ] _ "-" vowels ] .o.
    [ [ o | ó ] -> 0 || [ o | ó ] _ "-" vowels ] ] ;

# 11.    Vyi-{a,o}    ->    Vy{a,o}
# i-Loss 
define iLoss [
    [ [ i | í ] -> 0 || [ a | á | o | ó ] y _ [ a | á | o | ó ] ] .o.
    [ i y [ i | í ] -> i (i) y ,
      í y [ i | í ] -> í (í) y || _ [ a | á | o | ó ] ] ] ;

# 12.    si{a,o}    ->    s{a,o}
# i-Absorption
# ISSUE: the prominence on the deleted /i/ in this rule may persist;
# consider /w-mo'tsís-oaawa-istsi/ -> <o'tsóaawaistsi> from Frantz (1991, p. 69).
define iAbsorption [ i | í ] ("-") -> 0 || s _ [ a | á | o | ó ] ;

# 13.    sihs    ->    ss
# ih-Loss
define ihLoss [ i | í ] "-" h -> 0 || s _ s ;

# 14.    ihs    ->    ss
# Presibilation
# While Frantz (1991, fn. 167) points out speaker- and dialect-level variation
# in this rule, this is not captured by the current implementation.
define presibilation [ i | í ] "-" h -> s || _ s ;

# 15.    CG    ->    C    , where C ne "'"
# Semivowel Loss
define semivowelLoss glides -> 0 || obstruents "-" _ ;

# 16.    Ciyiy    ->    Ciiy
# y-Reduction (optional)
define yReduction [ y (->) 0 ||
    [ obstruents | "'" ] [ i | í ] _ [ i | í ] ("-") y ] ;

# 17.    sih    ->    ss
# Postsibilation
define postsibilation [ i | í ] ("-") h -> s || s _ ;

# 18.    ti    ->    tsi
# t-Affrication
define tAffrication "-" -> s || t _ [ i | í ] ;

# 19.    V'VC    ->    VV'C
# Glottal Metathesis
define glottalMetathesis [
    "'" "-" a -> "-" a "'" ,
    "'" "-" á -> "-" á "'" ,
    "'" "-" a a -> "-" a a "'" ,
    "'" "-" á á -> "-" á á "'" ,
    "'" "-" i -> "-" i "'" ,
    "'" "-" í -> "-" í "'" ,
    "'" "-" i i -> "-" i i "'" ,
    "'" "-" í í -> "-" í í "'" ,
    "'" "-" o -> "-" o "'" ,
    "'" "-" ó -> "-" ó "'" ,
    "'" "-" o o -> "-" o o "'" ,
    "'" "-" ó ó -> "-" ó ó "'" || vowels _ [ consonants | "'" | h ] ] ;


# 20.    VV1V1'C    ->    VV1V1C
# Glottal Loss
# Note that glottal loss does not occur when the long vowel is a variable
# length one (Frantz 1991, p. 154). This is an unresolved issue that will
# probably cause problems. Future work should reconcile this generalization
# with the definition of variableLengthVowels above.
define glottalLoss [
    a a "'" -> a a ,
    á á "'" -> á á ,
    i i "'" -> i i ,
    í í "'" -> í í ,
    o o "'" -> o o ,
    ó ó "'" -> ó ó || vowels ("-") _ consonants ] ;

# 21.    V'(s)CC    ->    VV(s)CC    , where C ne 's'
# Glottal Assimilation
define glottalAssimilation [
    a "'" -> a a ,
    á "'" -> á á ,
    i "'" -> i i ,
    í "'" -> í í ,
    o "'" -> o o ,
    ó "'" -> ó ó || _ (s) [ p p | t t | k k | m m | n n ] ] ;

# 22.    ''    ->    '
# Glottal Reduction
define glottalReduction "'" "'" -> "'";

# 23.    V1'h    ->    V1'V1h
# Vowel Epenthesis
# note: In place of this rule, some speakers have the following rule:
# ' -> 0 / _ h
define vowelEpenthesis [ 
    a "'" -> [ a "'" a | a ] ,
    á "'" -> [ á "'" á | á ] ,
    i "'" -> [ i "'" i | i ] ,
    í "'" -> [ í "'" í | í ] ,
    o "'" -> [ o "'" o | o ] ,
    ó "'" -> [ ó "'" ó | ó ] || _ h ] ;

# 24.    sssC    ->    ssC
# sss-Shortening
define sssShortening s -> 0 || _ s s [ stops | glides ] ;

# 25.
# Accent Spread
define accentSpread [
    a -> á ,
    i -> í ,
    o -> ó || accentedVowels "-" _ ] ;

# 26.    - -> 0
# Break-Delete
# Remove the morpheme delimiter symbol. Note that \"-" is the term
# complement of "-", i.e., the set of single-symbol strings that does
# not contain the hyphen. This formulation is superior to "-" -> 0 since
# it does not introduce so much ambiguity during parsing.
#define breakDelete "-" -> 0 || [ .#. | \"-" ] _ \"-" ;
define breakDelete "-" -> 0 ;



# Phonology FST: ordered rewrite rules = ordered composition of FSTs
################################################################################
#
# Rules apply for maximal ``feeding'' and minimal ``bleeding'' except that
# coalescence bleeds desyllabification, presibilation, and semivowel loss;
# i-absorption bleeds desyllabification, and gemination bleeds s-insertion.
# Desyllabification bleeds accent spread, but does not bleed nor feed any other
# rules (Frantz 1991, p. 155).

define phonology [

    # lexical phonological rules
    irregularBreaking .o.
    semivowelAlternation .o.
    nasalInitialVerbs .o.
    imperativeInitialVowelElision .o.
    codafication .o.
    stopStopEpenthesis .o.
    becomeO .o.
    becomeA .o.
    nasalLoss .o.
    variableLengthVowels .o.
    3mm .o.
    DTPVowelElision .o.
    nonPermanentConsonants .o.
    diphthongization .o.
    initialChangeAY .o.
    initialChangeII .o.
    yiLoss .o.
    inverseClipping .o.

    # (general) phonological rules
    coalescence .o. 
    semivowelLoss .o. 
    gemination .o. 
    sConnection .o. 
    yReduction .o. 
    breaking .o. 
    oReplacement .o. 
    ihLoss .o. 
    presibilation .o. 
    sssShortening .o. 
    semivowelDrop .o. 
    vowelShortening .o. 
    tAffrication .o. 
    postsibilation .o. 
    iAbsorption .o.
    desyllabification .o. 
    glottalMetathesis .o. 
    vowelEpenthesis .o. 
    glottalReduction .o. 
    glottalLoss .o. 
    glottalAssimilation .o. 
    accentSpread .o. 
    breakDelete .o. 
    iLoss 
] ;

