################################################################################
# The phonological rules of Frantz (1997) as FSTs *with Additional Rules*
################################################################################

# How to understand this file
################################################################################

# - "A -> B || C _ D" means "rewrite A as B only when it occurs between C and D"
# - "(->)" is optional rewrite
# - "define name expression" assigns the FSM/FST generated by "expression" to
#   "name"
# - Special characters (e.g., "-") need to be enclosed in quotes
# - ".o." denotes the composition operation
# - "[..]" denotes the empty symbol in a rewrite rule (using "0" in an insertion
#   rule will result in 1 or more (!) insertions

# Comments
################################################################################

# Some interpretation of the ordered rewrite rules of Frantz (1997) was
# required:
#
# - what to do with the morpheme segmentation symbol "-" in the rules
# - Frantz (1997) provides a partial ordering: some decisions had to be made

# Tests
################################################################################

# A line that begins with "#test" signifies a test.  After "#test" there should
# be a string of characters followed by "->" followed by another string of
# characters.  The first string is the underlying representation and the second
# is the anticipated surface representation.  The OLD can run a phonology script
# against its tests and return a response detailing which tests passed and which
# failed.




# Accent/Syllabification tests from Weber (2013)
################################################################################
#
# NOTE: Weber transcribes word-initial and word-final vowel shortening
# as well as accented short vowels becoming long ...

#test ikskimaa-wa               -> ikskímaa
#test it-ohkooni-m-wa           -> itohkóónim'a
#test ohkooni-t                 -> ohkóónit
#test it-oohko-yii-wa           -> itóóhkoyii
#test oohko-s                   -> oohkós
#test ipasskaa-wa               -> ipásskaa
#test miistsis                  -> miistsís
#test ot-áaksisaa               -> otáaksisaa
#test ipon-i-oo-atw-yii-wa      -> ipónyowatsiiwa
#test ipon-i-oo-atw-yii-wa      -> ipónyoatsiiwa
#test nit-ii-yooht              -> nitsííyooht
#test atw-i-hsi                 -> atohsi
#test i-ssi-ika-aa'si           -> issííkaa'si
#test i-pookáá-wa'si            -> ipookááwa'si
#test á-iik-isamo               -> áííksisamo
#test m-ááhk-aanist-opam-oo-at  -> mááhkaanistopamaat
#test nit-á-sstss-i-aaki        -> nitásstssyaaki
#test sik-ika-ikoan             -> siksikáíkoan
##test sik-ika-ikoan            -> siksikaikoan
#test ti-hp                     -> ti'p
#test ot-i-inaani               -> otsíínaan
#test nit-á-okstoo-hp-wa        -> nitáókstoohp
#test ot-ihkiitaa-n-istsi       -> otsskíítaanistsi
#test n-ko's                    -> nohko's
#test á-sstsina'-waasai'ni-wa   -> áísstsinaa'sai'niwa
#test nit-i'poyi-hpinnaan       -> nitsi'poy'ihpinnaan
#test kit-i'poyi-hpinnaan       -> kitsí'poy'ihpinnaan
#test iik-it-ssto-yi            -> iikítsstoyi
#test nit-a'-ipiitsiy-áak-okska'si -> nitáí'piitsiyáakokska'si
#test nit-i-inaani              -> nitsíínaani
#test kit-inaani                -> kitsinaan'i
#test nit-iik-sopo-a'p          -> nitsiikssopoya'p
#test ipon-oo-at                -> iponowat
#test nit-ipo-i'po-yi           -> nitsipoyi'poyi
#test nit-iik-sopo-a'p-I        -> nitsiikssopa'psi
#test kit-á-simi                -> kitáísim'i
#test kit-iik-sopo-a'p          -> kitsííkssopoya'p
#test ipon-oo-at-yii            -> iponówatsii
#test kit-ipo-i'po-yi           -> kitsipóyi'poyi
#test kit-iik-sopo-a'p-I        -> kitsííkssopa'psi
#test nit-ii-yooht-o-aa         -> nitsííyoohtoaa
#test apw-i                     -> api
#test sa-oht-yi                 -> saohtsi
#test nit-ssksinoawa            -> nitssksinoawa
#test á-okska'siwa              -> áókska'siwa
#test atsikí-istsi              -> atsikíístsi
#test kakkóó-iksi               -> kakkóíksi
#test nit-ihpiyi                -> nitsspiyi
#test káta'-ottakiwaatsi        -> kátaoottakiwaatsi
#test á'-isttohkohpiy'ssi       -> áíísttohkohpiy'ssi
#test á'-o'tooyiniki            -> áó'tooyiniki
#test káta'-ohto'toowa          -> kátao'ohto'toowa
#test káta'-ohto'toowa          -> kátaohto'toowa
#test á-ihpiyi-yináyi           -> áíhpiyiyináyi
#test á-okska'si-hpinnaan       -> áókska'sspinnaan
#test nit-it-itsiniki           -> nitsitsitsiniki
#test kit-it-itsiniki           -> kitsitsítsiniki
#test á'-omai'taki-wa           -> áó'mai'takiwa
#test káta'-ookaawaatsi         -> kátaookaawaatsi
#test pii-hsini                 -> pissini
#test áak-yaatoowa              -> áakaatoowa
#test nit-waanii                -> nitáánii
#test kit-waanii                -> kitaanii
#test kikáta'-waaniihpa         -> kikáta'waaniihpa
#test áíhpiyi-yináyi            -> áíhpiiyináyi
#test á-okstaki-yi-aawa         -> áókstakiyaawa
#test á-okska'si-o'pa           -> áókska'so'pa
#test nit-Ioyi                  -> nitsoyi
#test kit-Ioyi                  -> kitsoyí
#test otokska'si-hsi            -> otokska'ssi
#test otá'po'taki-hsi           -> otá'po'takssi
#test á-yo'kaa-o'pa             -> áyo'kao'pa
#test imitáá-iksi               -> imitáíksi
#test á-yo'kaa-yi-aawa          -> áyo'kaayaawa
#test á-ihpiyi-o'pa             -> áíhpiyo'pa
#test á-okstaki-yi-aawa         -> áókstakiiyaawa
#test kit-i'powata-oaawa        -> kitsí'powatawaawa
#test á-Io'kaa-wa               -> áyo'kaawa
#test yaatoo-t                  -> aatóót
#test waanii-t                  -> aaníít
#test w-oko'si                  -> oko'si
#test áak-oto-apinnii-wa        -> áakotaapinniiwa
#test áak-oto-apinnii-wa        -> áakotapinniiwa
#test w-inn-yi                  -> onni
#test w-iihsiss-yi              -> ohsíssi
#test áak-Ipiima                -> áaksipiima
#test nit-waanIt-k-wa           -> nitaanikka
#test nit-waanIt-aa-wa          -> nitaanistaawa
#test nit-siksip-aa-wa          -> nitssiksipaawa
#test nit-ssikópii              -> nitsssikópii
#test á-sínaaki-wa              -> áísínaakiwa
#test nikáá-ssikópii            -> nikáíssikópii
#test káta'-simi-wa             -> kátai'simiwa
#
#
#
################################################################################
# Tests that will fail
################################################################################
#
# Initial change
################################################################################
#
##test n-i-ákáá-ssi-i-hp-wa      -> nikáíssii'pa
##test n-asóka'sim-yi            -> nisóóka'simii
##test ooyi                      -> iiyi
##test nit-yooht-o-aa            -> nitsííyoohtoaa
#
# Note the apparent contradiction in the following two mappings:
#
##test nit-yáapi                 -> nitsaapi
##test nit-yáapi                 -> nitsíaapi
#
# More mappings where IC is "affricaion-al":
#
##test nit-yo'kaa                -> nitso'kaa
##test nit-ooyi                  -> nitsoyi
##test nit-áak-yo'kaa            -> nitáakso'kaa
##test nit-áak-ooyi              -> nitáaksoyi
#
define gloss
define initialChange [
    
#
# Initial change changes lexical accenting
################################################################################
#
##test nit-i-yáapi               -> nitsíaapi
#
# The above could be explained by assuming that IC inserts long /i/ in this case.
# Then /nit-ii-yáapi/ would phonologize/syllabify to [ni.tsii.yáapi] and we could
# alter the default prominence rules to ignore and reduce falling contours...
#
# A similar, but simpler, story could be told about the following ...
#
##test nit-i-yooht               -> nitsííyooht
##test ot-inaani                 -> otsínaan
##test nit-inaani -> nitsínaani
#
# How to explain the following given the first test in this section?
#
##test nit-i-yáapi               -> nitsaapi
#
#
# Default accent changes vowel length
################################################################################
#
##test miistsis                   -> mistsíís
#
#
# Falling contour shortening
################################################################################
#
##test ot-áaksisaa               -> otáksisaa
#
#
# Vowel transcribed where (assumedly) glide should have been
################################################################################
#
##test ipon-i-oo-atw-yii-wa      -> ipóniowatsiiwa
#
#
# Glottal Loss expected to apply
################################################################################
#
##test i-ssi-ika-aa'si           -> issííkaasi
#
#
# Default accent on syllable one -- related to IC?
################################################################################
#
# Intuitively I would predict the following mapping (find *real* evidence!)
# and I think the reason for the accent on syllable one is that it is the only
# heavy accentable syllable among the first three.
#
##test nit-ssksinoawa            -> nitssksinoawa
#
# This phonology would predict the following default accent, which is probably
# wrong:
#
##test kit-Ioyi                  -> kitsoyí
##test w-oko'si                 -> óko'si
#
#
# Accented long diphthong alternate transcription convention
################################################################################
#
##test á'-isttohkohpiy'ssi       -> áíisttohkohpiy'ssi
#
#
#
define graphs                   [ p | t | k | m | n | s | w | y | h | "'" | a | i | o | á | í | ó ] ;
define vowels                   [ a | i | o | á | í | ó ] ;
define accentedVowels           [ á | í | ó ] ;
define unaccentedVowels         vowels - accentedVowels ;
define unaccentedGraphs         graphs - accentedVowels ;
define nasals                   [ m | n ] ;
define glides                   [ w | y ] ;
define plosives                 [ p | t | k ] ;
define fricatives               [ s | h ] ;
define affricates               [ t s | k s ] ;
define geminates                [ p p | t t | k k | m m | n n ] ;
define vocoids                  vowels | s ;
define obstruents               plosives | nasals | s ;
define stops                    plosives | "'" ;
define sonorants                nasals | glides ;
define buccalSimplexConsonants  obstruents | glides ;
define simplexConsonants        stops | nasals | fricatives | glides ;
define consonants               simplexConsonants | affricates ;


# 1.    C1-C2     ->     C2C2
# Gemination
define gemination plosives "-" -> p || _ p .o.
                  plosives "-" -> t || _ t .o.
                  plosives "-" -> k || _ k ;

# 2.    It    ->    Ist
# s-Insertion (assumes that "breaking I" is a phoneme)
define sInsertion [..] -> s || I _ t ;

# 3.a.    C-s    ->    Css
# s-Connection A
define sConnectionA "-" -> s || [ plosives | nasals ] _ s ;

# 3.b.    V(')-s    ->    V(')-is
# s-Connection B
# condition: where 's' is not part of a suffix
# present implementation: rule is optional
define sConnectionB [..] (->) i || vowels ("'") "-" _ s ;

# 4.    o-a    ->    aa
# New o-Replacement
# note: for some speakers the o is deleted
# condition: where 'a' is not part of a suffix
# present implementation: rule is optional
define oReplacement [
    o o           (->) [ a a | a | á á | á ] ,
    o             (->) [ a | 0 ] ,
    ó ó           (->) [ á á | á ] ,
    ó             (->) [ á | 0 ]                    || _ "-" [ a | á ] ] ;

# 5.    w-i(i)    ->    o
# New Coalescence
define coalescence [
    w "-" i (i) -> o ,
    w "-" í (í) -> ó || _ ("-") simplexConsonants ] ;

# 6.    k-I    ->    ksi,    k-i    ->    ksi, optionally
define breaking "-" -> s   || k _ I .o.
                "-" (->) s || k _ [i | í] , p _ I ;

# 7.    I -> i
# Neutralization
define neutralization I -> i ;

# 8.a.    V-iV    ->    VyV
# Desyllabification A
# Note the alteration to this rule: now glides form optionally between C and V ...
define desyllabificationA "-" i -> y || vowels _ [ a | á | o | ó ] .o.
                          "-" i (->) y || obstruents _ ("-") [ a | á | o | ó ];

# 8.b.    CVoV    ->    CVwV
# Desyllabification B
define desyllabificationB o -> w   || buccalSimplexConsonants ("-") vowels ("-") _ ("-") [ a | á | i | í | ó ] .o.
                          "-" -> y || buccalSimplexConsonants o _ [ a | á | i | í | ó ] ;

# 9.    #G    ->    0
# Semivowel Drop
define semivowelDrop glides -> 0 || .#. _ ;

# 10.    V1V1-V    ->    V1V
# Vowel Shortening
define vowelShortening [ a | á ] -> 0 || [ a | á ] _ "-" vowels .o.
                       [ i | í ] -> 0 || [ i | í ] _ "-" vowels .o.
                       [ o | ó ] -> 0 || [ o | ó ] _ "-" vowels .o.
                       [ a | á ] -> 0 || [ a | á ] ("-") _ ("-") [ a | á ];

# 11.    Vyi-{a,o}    ->    Vy{a,o}
# i-Loss 
define iLoss [
    [ i | í ] -> 0               || [ a | á | o | ó ] y _ [ a | á | o | ó ] .o.
    i y [ i | í ] -> i (i) y ,
    í y [ i | í ] -> í (í) y     || _ [ a | á | o | ó ] ] ;

# 12.    si{a,o}    ->    s{a,o}
# i-Absorption
define iAbsorption [ i | í ] ("-") -> 0 || s _ [ a | á | o | ó ] ;

# 13.    sihs    ->    ss
# ih-Loss
define ihLoss [ i | í ] "-" h -> 0 || s _ s ;

# 14.    ihs    ->    ss
# Presibilation
define presibilation [ i | í ] "-" h -> s || _ s ;

# 15.    CG    ->    C    , where C ne "'"
# Semivowel Loss
define semivowelLoss y -> 0 || [ obstruents | I ] ("-") _ .o.
                     w -> 0 || obstruents ("-") _ ;

# 16.    Ciyiy    ->    Ciiy
# y-Reduction (optional)
define yReduction y (->) 0 || [ obstruents | "'" ] [ i | í ] _ [ i | í ] ("-") y ;

# 17.    sih    ->    ss
# Postsibilation
define postsibilation [ i | í ] ("-") h -> s || s _ ;

# 18.    ti    ->    tsi
# t-Affrication
define tAffrication "-" -> s || t _ [ i | í ] ;

# 19.    V'VC    ->    VV'C
# Glottal Metathesis
define glottalMetathesis w (->) 0 || "'" "-" _       .o. [
                         "'" "-" a -> "-" a "'"      ,
                         "'" "-" á -> "-" á "'"      ,
                         "'" "-" a a -> "-" a a "'"  ,
                         "'" "-" á á -> "-" á á "'"  ,
                         "'" "-" i -> "-" i "'"      ,
                         "'" "-" í -> "-" í "'"      ,
                         "'" "-" i i -> "-" i i "'"  ,
                         "'" "-" í í -> "-" í í "'"  ,
                         "'" "-" o -> "-" o "'"      ,
                         "'" "-" ó -> "-" ó "'"      ,
                         "'" "-" o o -> "-" o o "'"  ,
                         "'" "-" ó ó -> "-" ó ó "'"         || vowels _ simplexConsonants ] ;

# 20.    VV1V1'C    ->    VV1V1C
# Glottal Loss
define glottalLoss [
    a a "'" -> a a ,
    á á "'" -> á á ,
    i i "'" -> i i ,
    í í "'" -> í í ,
    o o "'" -> o o ,
    ó ó "'" -> ó ó       || vowels ("-") _ buccalSimplexConsonants ] ;

# 21.    V'(s)CC    ->    VV(s)CC    , where C ne 's'
# Glottal Assimilation
define glottalAssimilation [
    a "'" -> a a ,
    á "'" -> á á ,
    i "'" -> i i ,
    í "'" -> í í ,
    o "'" -> o o ,
    ó "'" -> ó ó     || _ (s) geminates ] ;

# 22.    ''    ->    '
# Glottal Reduction
define glottalReduction "'" "'" -> "'" ;

# 23.    V1'h    ->    V1'V1h
# Vowel Epenthesis
# note: In place of this rule, some speakers have the following rule:
# ' -> 0 / _ h
define vowelEpenthesis [
    a "'" -> [ a "'" a | a ] ,
    á "'" -> [ á "'" á | á ] ,
    i "'" -> [ i "'" i | i ] ,
    í "'" -> [ í "'" í | í ] ,
    o "'" -> [ o "'" o | o ] ,
    ó "'" -> [ ó "'" ó | ó ]    || _ h ] ;

# 24.    sssC    ->    ssC
# sss-Shortening
define sssShortening s -> 0 || _ s s [ plosives | nasals | glides ] ;

# 25.
# Accent Spread
define accentSpread [
    a -> á      ,
    a a -> á á  ,
    i -> í      ,
    i i -> í í  ,
    o -> ó      ,
    o o -> ó ó          || accentedVowels "-" _ ] ;

# 26.    - -> 0
# Break-Delete
define breakDelete "-" -> 0 ;

# 27.
# Resolution of Non-permanent Consonants (cf. Frantz 1997: 9).
# These consonants disappear before vowels, see p. 82 of grammar.
define resolveNonPermanentConsonants [
    [ N | M | S ] -> 0 || _ "-" vowels .o.
    N -> n ,
    M -> m ,
    S -> s             || _ [ "-" | .#. ] ] ;

# 28.
# Inverse o-Loss ("t-ok-" -> "t-k-")
define oLossINV o -> 0 || t "-" _ k "-" ;

# 29.
# Nasal loss (might be a more general rule...)
define nasalLoss [ m | n ] -> 0 || plosives "-" _ ;

# 30.   NVh -> N'Vh
# Sonorant glottalization (before voiceless nuclei, cf. Weber 2013, p. 3 and Taylor 1969)
# NOTE: it may be nessary to order this rule post-syllabification and use the syllable
# boundary marker in the context ...
define voicelessNucleus [ [ vowels [ h | .#. ] ] | s s ] ;
define sonorantGlottalization [
    m (->) "'"      || m _ voicelessNucleus .o.
    n (->) "'"      || n _ voicelessNucleus .o.
    [..] (->) "'"   || sonorants _ voicelessNucleus
] ;

# 31. Default syllable two prominence
# Optionally accents every vowel in syllable two.
# This is an attempt to model the very vague generalization that Blackfoot words
# tend to have prominence in syllable two.  One problem with this approach is that
# it does not "move" prominence from neighbouring syllables which is what, impressionistically,
# appears to be happening.
#
# Another idea: add prominence to syllable three if syll 2 is devoiced
#     users ot-ohkiimi-n                  -> otoohkííman
# phonology ot-ohkiimi-n                  -> otohkiimin
#
define preContext .#. simplexConsonants* vowels+ simplexConsonants+;
define postContext [ p | t | k | m | n | s | w | y | "'" | .#. | h ];
define postContextX [ p | t | k | m | n | s | w | y | "'" | .#. ];
define defaultSyllableTwoProminence a a (->) á á     || preContext _ postContext .o.
                                    á a (->) á á     || preContext _ postContext .o.
                                    a á (->) á á     || preContext _ postContext .o.
                                    a (->) á         || preContext _ postContextX .o.
                                    i i (->) í í     || preContext _ postContext .o.
                                    i í (->) í í     || preContext _ postContext .o.
                                    í i (->) í í     || preContext _ postContext .o.
                                    i (->) í         || preContext _ postContextX .o.
                                    o o (->) ó ó     || preContext _ postContext .o.
                                    o ó (->) ó ó     || preContext _ postContext .o.
                                    ó o (->) ó ó     || preContext _ postContext .o.
                                    o (->) ó         || preContext _ postContextX .o.
                                    a i i (->) á í í || preContext _ postContext .o.
                                    a i (->) á í     || preContext _ postContext .o.
                                    a o o (->) á ó ó || preContext _ postContext .o.
                                    a o (->) á ó     || preContext _ postContext .o.
                                    o i i (->) ó í í || preContext _ postContext .o.
                                    o i (->) ó í     || preContext _ postContext;


# Weber 2013 generalizations on lexically unaccented words.
# a. monosyllabic words are accented on the one and only syllable
# b. disyllabic words are accented on the second syllable.
# c. for words with three or more syllables, the second syllable is accented
#    if it is heavy and the third syllable otherwise
# d. ...unless the third syllable contains a voiceless nuclei, in which case the second
#    syllable is accented.

# Accent monosyllabic words. Cf. Weber 2013.
# NOTE: I have made this rule optional since I know that many users do not mark accent on 
# monsyllabic words.
# NOTE 2: This should also take care of disyllabics where one nucleus is [s]
define accentMonosyllabics a a (->) á á     ,
                           á a (->) á á     ,
                           # /a á/ is probably bisyllabic a á (->) á á     ,
                           a (->) á         ,
                           i i (->) í í     ,
                           i í (->) í í     ,
                           í i (->) í í     ,
                           i (->) í         ,
                           o o (->) ó ó     ,
                           # /o ó/ is probably bisyllabic o ó (->) ó ó     ,
                           ó o (->) ó ó     ,
                           o (->) ó         ,
                           a i i (->) á í í ,
                           a i (->) á í     ,
                           a o o (->) á ó ó ,
                           a o (->) á ó     ,
                           o i i (->) ó í í ,
                           o i (->) ó í     || .#. simplexConsonants* _ simplexConsonants* .#. ;

# Begin syllabification
################################################################################

define onset            consonants - h ;
define nucleus          [ vowels (vocoids) | s ] ;
define coda             [[ fricatives | "'" ] [ plosives | nasals ] ] |
                        consonants ;

define syllable         onset nucleus (coda) ;
define syllableNoOnset  nucleus (coda) ;

# Alter syllabification so that /s/ doesn't constitute the nucleus if
# it doesn't have to.
define dispreferNucleicS [
    "." -> 0               ||
        # Word-initial /s/ is extra-syllabic, but we remove the boundary so
        # that it does not appear to be a mono-segmental syllable.
        .#. s _ ,
        # It's preferable for /s/ to be a coda than the nucleus of an
        # onset-less syllable, i.e., [.s.]:
        vowels _ s "." .o.
        # /ks/ and /ts/ should be affricates in coda position over CV
        # syllables with [s] as nucleus.
        # vowels ([ s | "'" ]) _ affricates [ "." | .#. ] .o.
    # It's preferable for /s/ to be a coda than the onset of a
    # nucleus-as-/s/ syllable.
    "." s s "." -> s "." s || vowels _ ] ;

# Alter syllabification so that certain vowel sequences are split across two
# syllables.
define segmentVowelSequences [..] -> "." || o _ [ a | á | ó ],
                                            a _ [ á | ó ] ;

# Syllables with one of y, w, m, n or ' as onset and s as nucleus are to be
# avoided as violations of the sonority sequence (cf. above).
define sonoritySequencing [
    "." y   -> y "." ,
    "." w   -> w "." ,
    "." m   -> m ".",
    "." n   -> n ".",
    "." "'" -> "'" "."    || vowels _ s ] ;

#
# The syllabify FST syllabifies Blackfoot words.  A brief explanation follows the
# definition.
#
define syllabify [
    syllable        @> ... "." || _ syllable .o.
    syllableNoOnset @> ... "." || _ syllable .o.
    dispreferNucleicS .o.
    segmentVowelSequences .o.
    sonoritySequencing
] ;

# The first regex in `syllabify` appends "." to all minimal ON(C) syllables
# followed by an ON(C) syllable.  Thus /tapikáíimii/ would be transformed to
# "ta.pi.káíimii" because "ta" is a minimal ON(C) syllable followed by another
# ON(C) syllable "pi" which is itself a minimal ON(C) syllable followed by
# another, i.e., "ká".  However, while both "ká" and "káí" are ON(C) syllables,
# neither is followed by another valid such syllable since the next segment is a
# vowel.  The second regex then transforms "ta.pi.káíimii" into [ta.pi.káíi.mii]
# (the correct parse) because it appends "." to the minimal (O)N(C) syllable "i"
# since it is followed by a minimal ON(C) syllable, viz. "mi".  Regexes #3-5 clean
# up the resulting parse in the ways described in the comments above their
# definitions.
#
# End syllabification
################################################################################


# Accent disyllabic words -- accent syllable two
################################################################################
#
define accentDisyllabics [
    a a   (->) á á     ,
    á a   (->) á á     ,
    a á   (->) á á     ,
    a     (->) á       ,
    i i   (->) í í     ,
    i í   (->) í í     ,
    í i   (->) í í     ,
    i     (->) í       ,
    o o   (->) ó ó     ,
    o ó   (->) ó ó     ,
    ó o   (->) ó ó     ,
    o     (->) ó       ,
    a i i (->) á í í   ,
    a i   (->) á í     ,
    a o o (->) á ó ó   ,
    a o   (->) á ó     ,
    o i i (->) ó í í   ,
    o i   (->) ó í     || .#. unaccentedGraphs+ "." consonants* _ consonants* .#. ] ;


# Accent trisyllabic+ words
################################################################################

# QUESTION: should accented long vowels before preaspirated stops be represented
# with an acute accent only on the first vowel as per Weber (2013) ?

define accentTrisyllabicsPlus [

    # Polyvocalic nuclei are accented ...
    a a   (->) á á     ,
    á a   (->) á á     ,
    a á   (->) á á     ,
    i i   (->) í í     ,
    i í   (->) í í     ,
    í i   (->) í í     ,
    o o   (->) ó ó     ,
    o ó   (->) ó ó     ,
    ó o   (->) ó ó     ,
    a i i (->) á í í   ,
    a i   (->) á í     ,
    a o o (->) á ó ó   ,
    a o   (->) á ó     ,
    o i i (->) ó í í   ,
    o i   (->) ó í     ||

    # ... in syllable two (if syllable three is not lexically accented)
    .#. unaccentedGraphs+
    "." consonants* _ consonants*
    "." unaccentedGraphs+ [ "." | .#. ] ,

    # ... or in syllable three if syllable two is voiceless or light.
    .#. unaccentedGraphs+
#    "." consonants* [ [ [ unaccentedVowels h ] | s ] consonants* ] | unaccentedVowels
    "." consonants* [ unaccentedVowels | s ] consonants*
    "." consonants* _ consonants* .o.

    # Monovocalic nuclei are accented ...
    i     (->)   í       ,
    a     (->)   á       ,
    o     (->)   ó       ||

    # ... in syllable two, if it has a coda consonant, i.e., is heavy,
    .#. unaccentedGraphs+
    "." consonants* _ [ consonants - h ]+ "." ,

    # ... in coda-less syllable two if syllable three has a voiceless nucleus,
    .#. unaccentedGraphs+
    "." consonants* _
    "." consonants* [ [ unaccentedVowels h ] | s ] consonants* [ "." | .#. ] ,

    # ... otherwise in syllable three (QUESTION: allow "short" diphthongs in unaccented syllable 2?)
    .#. unaccentedGraphs+
    "." consonants* [ unaccentedVowels (h) | s (s) ]
    "." consonants* _ [ ? - h ]* [ "." | .#. ]
] ;

define syllableDelimiterDelete "." -> 0 ;

# 32. Word-final vowel drop
# Kainai speakers drop word-final short vowels and their glide onsets.  These transformations
# are made optional because this phonology is still intended to cover all dialects.
# WARNING: this is a massive overgeneralization since only certain word-final vowels are
# dropped (cf. Denzer-King 2009), e.g., if the word-final vowel is a stem-final vowel, it 
# will not be dropped.  Will a noun-final vowel be dropped, e.g., "onni"
define wordFinalVowelDrop vowels    (->) 0    || buccalSimplexConsonants _ .#. .o.
                          [ y | w ] (->) 0    || _ .#. ;

# 33. Velar pre-aspiration
# NOTE: this is probably lexically determined epenthesis, see Weber (2013).
define velarPreaspiration "-" -> o h || [ m | n ] _ k ;

# 34. Glottal stoppification
define glottalStoppification h (->) "'" || "-" _ ;





# Initial change
# ################################################################################
# 
# Options:
#
# 1. new IC lexical item 
# 2. category-aware phonology
#
#     users nit-yooht-o-aa                -> nitsííyoohtoaa
# phonology nit-yooht-o-aa                -> nitoohtaa nitoohtaaa nitoohtoaa
# 
#     users ssi-ika-aa'si                 -> issííkaasi
# phonology ssi-ika-aa'si                 -> ssiikaaasi
# 
#     users n-asóka'sim-yi               -> nisóóka'simii
# phonology n-asóka'sim-yi               -> nasóka'simi
# 
#     users pookáá-wa'si                -> ipokaawa'si
# phonology pookáá-wa'si                -> pookááwa'si
# 
#     users n-ákáá-ssi-i-hp-wa         -> nikáíssii'pa
# phonology n-ákáá-ssi-i-hp-wa         -> nákáássiihpa nákáíssiihpa
# 
#     users ipon-oo-atw-yii-wa            -> ipóniowatsiiwa
# phonology ipon-oo-atw-yii-wa            -> iponoaatwyiiwa iponoatwyiiwa
# 
#     users ot-ksááhko-m-yi             -> otssksááhkoomi
# phonology ot-ksááhko-m-yi             -> okksááhkomi
# 
#     users nit-yáak-iksi-i-hp-wa        -> nitsíyaaksiksi'pa
# phonology nit-yáak-iksi-i-hp-wa        -> nitáakiksiihpa
# 
#     users nit-yooht-o-aa               -> nitsííyoohtoaa
# phonology nit-yooht-o-aa               -> nitoohtoaa, nitoohtaa, nitoohtaa
#
#     users ssi-ika-aa'si                 -> issííkaasi
# phonology ssi-ika-aa'si                 -> ssiikaaasi
#
#     users áka-piikáni-wa              -> ákaipiikániwa
# phonology áka-piikáni-wa              -> ákapiikániwa
#
#     users iit-kaanaisskiinaa-attsi-yii-wa-áyi-> iitsikánaiskiinawattsiwáy
# phonology iit-kaanaisskiinaa-attsi-yii-wa-áyi-> iikkaanaisskiinaattsiyiiwaáyi
#
#     users ooyi                         -> iiyi
# phonology ooyi                         -> ooy'i, ooyi, ooy, oo
#
#     users nit-yo'kaa                   -> nitso'kaa
# phonology nit-yo'kaa                   -> nito'kaa
#
# - is the following IC or breaking resulting from a phoneme, i.e., /Iooyi/, cf.
# Frantz (1971):
# 
#     users áak-ohk-ooyi                 -> aakohksooyi
# phonology áak-ohk-ooyi                 -> áakohkooyi
# 
# - is IC implicated in the following?:  (Interesting)
# 
#     users iksow-áwákaasii             -> ksisówáwakaasi
# phonology iksow-áwákaasii             -> iksowáwákaasii
# 
# 
# Inalienably possessed /m-/ prefix -- remove it from lexical items?
# ################################################################################
# 
# NOTE: this will require lexical changes or phonologies that can see categorial and
# semantic information (if that will even be enough...)
#
#     users ot-áaksisaa-moápssp-yi      -> otáksisawa'psspii
# phonology ot-áaksisaa-moápssp-yi      -> otáaksisaamoápsspi
# 
# - I incorrectly use /ot-/ as the third person nominal possessor prefix.  I should
#   use /w-/.
# 
#     users ot-mo'tokáán                -> o'tokán
# phonology ot-mo'tokáán                -> otmo'tokáán
# 
# 
# Mistranscription/shortening of geminates, long vowels and accented vowels
# ################################################################################
#
# NOTE: see "misspell" rule below.
# WARNING: this is a horrible transducer that will generate thousands of surface
# transcriptions for any given morpheme sequence input.  It should not be used!
#
# Singletons and geminates can be shortened or lengthened and vowels can be accented
# or deaccented.
define misspell p (p) (->) p (p) .o.
                t (t) (->) t (t) .o.
                k (k) (->) k (k) .o.
                m (m) (->) m (m) .o.
                n (n) (->) n (n) .o.
                s (s) (->) s (s) .o.
                [a | á] ([a | á]) (->) [a | á] ([a | á]) .o.
                [i | í] ([i | í]) (->) [i | í] ([i | í]) .o.
                [o | ó] ([o | ó]) (->) [o | ó] ([o | ó]);

#

#     users n-asóka'sim-yi               -> nisóóka'simii
# phonology n-asóka'sim-yi               -> nasóka'simi
# 
#     users miistsis                      -> mistsíís
# phonology miistsis                      -> miistsis
#
#     users ot-áaksisaa-moápssp-yi      -> otáksisawa'psspii
# phonology ot-áaksisaa-moápssp-yi      -> otáaksisaamoápsspi
# 
#     users á-ssi-ist-st-aaki            -> áíssiiststákii
# phonology á-ssi-ist-st-aaki            -> áíssiistsstaaki ássiistsstaaki
# 
#     users ni'tókskaa-m                 -> ni'tóókskam
# phonology ni'tókskaa-m                 -> ni'tókskaam
# 
#     users ot-ohkiimi-n                  -> otoohkííman
# phonology ot-ohkiimi-n                  -> otohkiimin
# 
#     users nit-á-okstoo-hp-wa           -> nitaoksstoopa
# phonology nit-á-okstoo-hp-wa           -> nitáókstoohpa
# 
#     users ot-ksááhko-m-yi             -> otssksááhkoomi
# phonology ot-ksááhko-m-yi             -> okksááhkomi
# 
#     users nit-yáak-iksi-i-hp-wa        -> nitsíyaaksiksi'pa
# phonology nit-yáak-iksi-i-hp-wa        -> nitáakiksiihpa
# 
#     users ot-áaksisaa                 -> otáksisaa
# phonology ot-áaksisaa                 -> otáaksisaa
# 
#
# 
# ***Default prominence***
# ################################################################################
# 
# NOTE: see the "defaultProminence" rule below.
# NOTE #2: this is a very crude and unsatisfactory "solution" to the issue...
# NOTE: I am *not* including this in the phonology at this point since the even
# cruder "misspell" rule should take care of this ...
#
define defaultProminence a (->) á ,
                         i (->) í ,
                         o (->) ó ;
#
#
#     users nit-a'-ipiitsiy-áak-okska'si -> nitáí'piitsiyáakokska'si
# phonology nit-a'-ipiitsiy-áak-okska'si -> nitai'piitsiyáakokska'si
# 
#     users iik-it-ssto-yi                -> iikítsstoyi
# phonology iik-it-ssto-yi                -> iikitsstoyi
# 
#     users nit-inaani                    -> nitíínaan nitsinaan'i nitsináán nitsíínaan nitsíínaan'i nitsínaan nitsínaani
# phonology nit-inaani                    -> nitsinaani
#
#     users waatsímma'pssi-wa            -> áátsimma'pssiwa
# phonology waatsímma'pssi-wa            -> aatsímma'pssiwa
# 
#     users aakíí                       -> aakii
# phonology aakíí                       -> aakíí
# 
#     users miistsis                      -> mistsíís
# phonology miistsis                      -> miistsis
# 
#     users á-ssi-ist-st-aaki            -> áíssiiststákii
# phonology á-ssi-ist-st-aaki            -> áíssiistsstaaki ássiistsstaaki
# 
#     users imitáá-wa                   -> imítaawa
# phonology imitáá-wa                   -> imitááwa
# 
#     users ot-ohkiimi-n                  -> otoohkííman
# phonology ot-ohkiimi-n                  -> otohkiimin
# 
#     users nit-á-okstoo-hp-wa           -> nitaoksstoopa
# phonology nit-á-okstoo-hp-wa           -> nitáókstoohpa
# 
#     users ot-ihkiitaa-n-istsi           -> otsskíítaanistsi
# phonology ot-ihkiitaa-n-istsi           -> otsskiitaanistsi
# 
# - a lexical problem in the following form too (?):
# 
#     users nit-ohkoitapiiyi-hsin-yi      -> nitohkóítapiiwahsini
# phonology nit-ohkoitapiiyi-hsin-yi      -> nitohkoitapiiyssini
# 
#     users iik-ikkak-yi                  -> iiksíkkaki
# phonology iik-ikkak-yi                  -> iikikkaki
# 
#     users it-iinohk-opii-wa             -> itsíínohkópiiwa
# phonology it-iinohk-opii-wa             -> itsiinohkopiiwa
#
#     users kit-á-simi                   -> kitáísim'i
# phonology kit-á-simi                   -> kitáísimi kitásimi
# 
#     users nit-inaani                    -> nitíínaan nitsinaan'i nitsináán nitsíínaan nitsíínaan'i nitsínaan nitsínaani
# phonology nit-inaani                    -> nitsinaani
# 
# It can include the glide onset as well:
# 
#     users ayak-o'too-yi-aawa            -> ayaako'toyaa
# phonology ayak-o'too-yi-aawa            -> ayako'tooyaawa
# 
# Or not ...
# 
#     users omó'tsi-aa-yi-aawa           -> áomótsaiaw
# phonology omó'tsi-aa-yi-aawa           -> omó'tsaayaawa
# 
# 
# Possessed nominals and "initial change"
# ################################################################################
# 
# NOTE: I am going to avoid dealing with this phenomenon until I have more data on 
# it.  Two ways of potentially handling it:
# 
# 1. /imitáá/ is actually /wimitáá/ (or something like that)
# 2. /nit/ and /kit/ on nominals are actually /nitw/ and /kitw/
# 
#     users nit-imitáá-m-wa             -> nitómitaama
# phonology nit-imitáá-m-wa             -> nitsimitááma
# 
# 
# 
#
# /oo/ "go" allomorphy ? ...
# ################################################################################
# 
# NOTE: I need more information on the behaviour of this morpheme before I go 
# positing phonological transformations for what might be a lexical issue.
#
#     users ipo-oo                        -> ipowá
# phonology ipo-oo                        -> ipooo
# 
# 
# Cross-glide glottal metathesis
# ################################################################################
# 
# NOTE: see the alteration to the glottalMetathesis rule.
#
#     users á-sstsina'-waasai'ni-wa      -> áísstsinaa'sai'niwa
# phonology á-sstsina'-waasai'ni-wa      -> áísstsina'waasai'niwa ásstsina'waasai'niwa
# 
# 
# Breaking extended
# ################################################################################
# 
# NOTE: see the alteration to the existing rule "breaking".
#
# I think that Armoskaite found that this was constrained by morphological domain ...
#
#
#     users sik-ika-ikoan                 -> siksikáíkoan
# phonology sik-ika-ikoan                 -> sikikaikoan
# 
#     users iik-i'pii                     -> iiksi'pii
# phonology iik-i'pii                     -> iiki'pii
# 
#     users iik-ómahk-inaattsi           -> iikómahksinattsi
# phonology iik-ómahk-inaattsi           -> iikómahkinaattsi
# 
#     users iik-ikkak-yi                  -> iiksíkkaki
# phonology iik-ikkak-yi                  -> iikikkaki
# 
#     users á-iik-isamo                  -> áiiksisamó
# phonology á-iik-isamo                  -> áíikisamo
# 
# 
# Complex coda resolution: insert /i/ (?)
# ################################################################################
# 
# NOTE: there might be some more morphology here.  I'm gonna hold off on this one.
#
#     users ipak-mo'tsís-m               -> ipakóttsisim
# phonology ipak-mo'tsís-m               -> ipakmo'tsísm
# 
# 
# Glottal stoppification
# ################################################################################
#
#     users káta'-itstsii-hp             -> kátai'tstsi'p
# phonology káta'-itstsii-hp             -> kátai'tstsiihp
# 
#     users m-ááhk-aanist-opam-oo-atw-i-hsi-> mahkanistopamato'si
# phonology m-ááhk-aanist-opam-oo-atw-i-hsi-> mááhkaanistopamoaatwssi mááhkaanistopamoatwssi
# 
#     users n-ákáá-ssi-i-hp-wa         -> nikáíssii'pa
# phonology n-ákáá-ssi-i-hp-wa         -> nákáássiihpa nákáíssiihpa
# 
#     users nit-yáak-iksi-i-hp-wa        -> nitsíyaaksiksi'pa
# phonology nit-yáak-iksi-i-hp-wa        -> nitáakiksiihpa
# 
# 
# Coalescence needs alteration
# ################################################################################
# 
# NOTE: see alteration to "coalescence" rule.
#
#     users m-ááhk-aanist-opam-oo-atw-i-hsi-> mahkanistopamato'si
# phonology m-ááhk-aanist-opam-oo-atw-i-hsi-> mááhkaanistopamoaatwssi mááhkaanistopamoatwssi
# 
# 
# /oo-a/ -> [aa] ???
# ################################################################################
# 
# NOTE: see alteration to "oReplacement" rule.
#
#     users m-ááhk-aanist-opam-oo-atw-i-hsi-> mahkanistopamato'si
# phonology m-ááhk-aanist-opam-oo-atw-i-hsi-> mááhkaanistopamoaatwssi mááhkaanistopamoatwssi
# 
# 
# desyllabificationA extended: /iV/ -> [yV] optionally (spelling variation)
# ################################################################################
# 
# NOTE: see alteration to the rule "desyllabificationA".
#
#     users nit-á-sstss-i-aaki           -> nitásstssyaaki
# phonology nit-á-sstss-i-aaki           -> nitáísstssiaaki nitásstssiaaki
# 
#     users omó'tsi-aa-yi-aawa           -> áomótsaiaw
# phonology omó'tsi-aa-yi-aawa           -> omó'tsaayaawa
# 
# 
# ??? /s/-elision after INV-/o/-loss (?)
# ################################################################################
# 
# What is going on here?  For one thing, it involves what I call in my phonology
# "inverse /o/ loss".  But why is the /s/ disappearing too?  Could this /is/ sequence
# really be the /I/ that "pre-breaks" a following /t/, as in Frantz (1971) where
# /aanist/ is transcribed as /aanIt/?...
# 
# NOTE: decided to hold off on writing a rule for this until more data come to light.
#
#     users nit-inn-aapiksist-ok-wa       -> nitsínnáápiksikka
# phonology nit-inn-aapiksist-ok-wa       -> nitsinnaapiksiskka
# 
# 
# Glottal stop elision (?)
# ################################################################################
# 
# NOTE: I am unsure whether there are enough data to motivate the positing of such
# a rule, so I will hold off on this one.  This is probably just researcher mistranscription.
#
#     users omó'tsi-aa-yi-aawa           -> áomótsaiaw
# phonology omó'tsi-aa-yi-aawa           -> omó'tsaayaawa
# 
#
# Accent spread not spreading far enough?
# ################################################################################
# 
# NOTE: see alteration to the "accentSpread" rule.
# NOTE: what does a sequence like [aii] mean?  Should it be reduced to [ai]?
#
#     users á-iik-isamo                  -> áiiksisamó
# phonology á-iik-isamo                  -> áíikisamo
# 
#
define phonology oLossINV .o.
    velarPreaspiration .o.
    resolveNonPermanentConsonants .o.
    glottalStoppification .o.
    coalescence .o.
    semivowelLoss .o. semivowelLoss .o.
    gemination .o.
    sInsertion .o.
    sConnectionB .o.
    yReduction .o.
    breaking .o.
    oReplacement .o.
    ihLoss .o.
    sConnectionA .o.
    presibilation .o.
    sssShortening .o.
    semivowelDrop .o.
    neutralization .o.
    tAffrication .o.
    postsibilation .o.
    iAbsorption .o.
    desyllabificationB .o.
    desyllabificationA .o.
    glottalMetathesis .o.
    vowelShortening .o.
    vowelEpenthesis .o.
    glottalReduction .o.
    glottalLoss .o.
    glottalAssimilation .o.
    accentSpread .o.
    breakDelete .o.
    iLoss .o.
    wordFinalVowelDrop .o.
    sonorantGlottalization .o.
    accentMonosyllabics .o.
    syllabify .o.
    accentDisyllabics .o.
    accentTrisyllabicsPlus .o.
    syllableDelimiterDelete ;



# BACKUP OF SYLLABIFICATION AND ACCENT-PLACEMENT CODE
################################################################################

# Weber 2013 generalizations on lexically unaccented words.
## a. monosyllabic words are accented on the one and only syllable
## b. disyllabic words are accented on the second syllable.
## c. for words with three or more syllables, the second syllable is accented
##    if it is heavy and the third syllable otherwise
## d. ...unless the third syllable contains a voiceless nuclei, in which case the second
##    syllable is accented.
#
## Accent monosyllabic words. Cf. Weber 2013.
## NOTE: I have made this rule optional since I know that many users do not mark accent on 
## monsyllabic words.
## NOTE 2: This should also take care of disyllabics where one nucleus is [s]
#define accentMonosyllabics a a (->) á á     ,
#                           á a (->) á á     ,
#                           # /a á/ is probably bisyllabic a á (->) á á     ,
#                           a (->) á         ,
#                           i i (->) í í     ,
#                           i í (->) í í     ,
#                           í i (->) í í     ,
#                           i (->) í         ,
#                           o o (->) ó ó     ,
#                           # /o ó/ is probably bisyllabic o ó (->) ó ó     ,
#                           ó o (->) ó ó     ,
#                           o (->) ó         ,
#                           a i i (->) á í í ,
#                           a i (->) á í     ,
#                           a o o (->) á ó ó ,
#                           a o (->) á ó     ,
#                           o i i (->) ó í í ,
#                           o i (->) ó í     || .#. simplexConsonants* _ simplexConsonants* .#. ;
#
## Begin syllabification
#################################################################################
#
#define onset            consonants - h ;
#define nucleus          [ vowels (vocoids) | s ] ;
#define coda             [[ fricatives | "'" ] [ plosives | nasals ] ] |
#                        consonants ;
#
#define syllable         onset nucleus (coda) ;
#define syllableNoOnset  nucleus (coda) ;
#
## Alter syllabification so that /s/ doesn't constitute the nucleus if
## it doesn't have to.
#define dispreferNucleicS [
#    "." -> 0               ||
#        # Word-initial /s/ is extra-syllabic, but we remove the boundary so
#        # that it does not appear to be a mono-segmental syllable.
#        .#. s _ ,
#        # It's preferable for /s/ to be a coda than the nucleus of an
#        # onset-less syllable, i.e., [.s.]:
#        vowels _ s "." .o.
#        # /ks/ and /ts/ should be affricates in coda position over CV
#        # syllables with [s] as nucleus.
#        # vowels ([ s | "'" ]) _ affricates [ "." | .#. ] .o.
#    # It's preferable for /s/ to be a coda than the onset of a
#    # nucleus-as-/s/ syllable.
#    "." s s "." -> s "." s || vowels _ ] ;
#
## Alter syllabification so that certain vowel sequences are split across two
## syllables.
#define segmentVowelSequences [..] -> "." || o _ [ a | á | ó ],
#                                            a _ [ á | ó ] ;
#
## Syllables with one of y, w, m, n or ' as onset and s as nucleus are to be
## avoided as violations of the sonority sequence (cf. above).
#define sonoritySequencing [
#    "." y   -> y "." ,
#    "." w   -> w "." ,
#    "." m   -> m ".",
#    "." n   -> n ".",
#    "." "'" -> "'" "."    || vowels _ s ] ;
#
##
## The syllabify FST syllabifies Blackfoot words.  A brief explanation follows the
## definition.
##
#define syllabify [
#    syllable        @> ... "." || _ syllable .o.
#    syllableNoOnset @> ... "." || _ syllable .o.
#    dispreferNucleicS .o.
#    segmentVowelSequences .o.
#    sonoritySequencing
#] ;
#
## The first regex in `syllabify` appends "." to all minimal ON(C) syllables
## followed by an ON(C) syllable.  Thus /tapikáíimii/ would be transformed to
## "ta.pi.káíimii" because "ta" is a minimal ON(C) syllable followed by another
## ON(C) syllable "pi" which is itself a minimal ON(C) syllable followed by
## another, i.e., "ká".  However, while both "ká" and "káí" are ON(C) syllables,
## neither is followed by another valid such syllable since the next segment is a
## vowel.  The second regex then transforms "ta.pi.káíimii" into [ta.pi.káíi.mii]
## (the correct parse) because it appends "." to the minimal (O)N(C) syllable "i"
## since it is followed by a minimal ON(C) syllable, viz. "mi".  Regexes #3-5 clean
## up the resulting parse in the ways described in the comments above their
## definitions.
##
## End syllabification
#################################################################################
#
#
## Accent disyllabic words -- accent syllable two
#################################################################################
##
#define accentDisyllabics [
#    a a   ->   á á     ,
#    á a   (->) á á     ,
#    a á   (->) á á     ,
#    a     ->   á       ,
#    i i   ->   í í     ,
#    i í   (->) í í     ,
#    í i   (->) í í     ,
#    i     ->   í       ,
#    o o   ->   ó ó     ,
#    o ó   (->) ó ó     ,
#    ó o   (->) ó ó     ,
#    o     ->   ó       ,
#    a i i ->   á í í   ,
#    a i   ->   á í     ,
#    a o o ->   á ó ó   ,
#    a o   ->   á ó     ,
#    o i i ->   ó í í   ,
#    o i   ->   ó í     || .#. unaccentedGraphs+ "." consonants* _ consonants* .#. ] ;
#
#
## Accent trisyllabic+ words
#################################################################################
#
## QUESTION: should accented long vowels before preaspirated stops be represented
## with an acute accent only on the first vowel as per Weber (2013) ?
#
#define accentTrisyllabicsPlus [
#
#    # Polyvocalic nuclei are accented ...
#    a a   ->   á á     ,
#    á a   (->) á á     ,
#    a á   (->) á á     ,
#    i i   ->   í í     ,
#    i í   (->) í í     ,
#    í i   (->) í í     ,
#    o o   ->   ó ó     ,
#    o ó   (->) ó ó     ,
#    ó o   (->) ó ó     ,
#    a i i ->   á í í   ,
#    a i   ->   á í     ,
#    a o o ->   á ó ó   ,
#    a o   ->   á ó     ,
#    o i i ->   ó í í   ,
#    o i   ->   ó í     ||
#
#    # ... in syllable two,
#    .#. unaccentedGraphs+
#    "." consonants* _ consonants* "." ,
#
#    # ... or in syllable three if syllable two is voiceless.
#    .#. unaccentedGraphs+
#    "." consonants* [ [ unaccentedVowels h ] | s ] consonants*
#    "." consonants* _ consonants* .o.
#
#    # Monovocalic nuclei are accented ...
#    i     ->   í       ,
#    a     ->   á       ,
#    o     ->   ó       ||
#
#    # ... in syllable two, if it has a coda consonant, i.e., is heavy,
#    .#. unaccentedGraphs+
#    "." consonants* _ [ consonants - h ]+ "." ,
#
#    # ... in coda-less syllable two if syllable three has a voiceless nucleus,
#    .#. unaccentedGraphs+
#    "." consonants* _
#    "." consonants* [ [ unaccentedVowels h ] | s ] consonants* [ "." | .#. ] ,
#
#    # ... otherwise in syllable three (QUESTION: allow "short" diphthongs in unaccented syllable 2?)
#    .#. unaccentedGraphs+
#    "." consonants* [ unaccentedVowels (h) | s (s) ]
#    "." consonants* _ [ ? - h ]* [ "." | .#. ]
#] ;
#
#define syllableDelimiterDelete "." -> 0 ;
#
#

