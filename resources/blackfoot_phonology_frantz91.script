################################################################################
# The phonological rules of Frantz (1997) as FSTs
################################################################################
#
#
# How to understand this file
################################################################################
#
# - "A -> B || C _ D" means "rewrite A as B only when it occurs between C and D"
# - "(->)" is optional rewrite
# - "define name expression" assigns the FSM/FST generated by "expression" to
#   "name"
# - Special characters (e.g., "-") need to be enclosed in quotes
# - ".o." denotes the composition operation
# - "[..]" denotes the empty symbol in a rewrite rule (using "0" in an insertion
#   rule will result in 1 or more (!) insertions
#
#
# Comments
################################################################################
#
# Some interpretation of the ordered rewrite rules of Frantz (1997) was
# required:
#
# - what to do with the morpheme segmentation symbol "-" in the rules
# - Frantz (1997) provides a partial ordering: some decisions had to be made
#
#
# Tests
################################################################################
#
# A line that begins with "#test" signifies a test.  After "#test" there should
# be a string of characters followed by "->" followed by another string of
# characters.  The first string is the underlying representation and the second
# is the anticipated surface representation.  The OLD can run a phonology script
# against its tests and return a response detailing which tests passed and which
# failed.

#test nit-waanIt-k-wa -> nitaanikka
#test nit-waanIt-aa-wa -> nitaanistaawa
#test nit-siksipawa -> nitssiksipawa
#test nit-ssikópii -> nitsssikópii
#test á-sínaaki-wa -> áísínaakiwa
#test nikáá-ssikópii -> nikáíssikópii
#test káta'-simi-wa -> kátai'simiwa
#test áak-oto-apinnii-wa -> áakotaapinniiwa
#test áak-oto-apinnii-wa -> áakotapinniiwa
#test w-ínni -> ónni
#test w-iihsíssi -> ohsíssi
#test áak-Ipiima -> áaksipiima
#test kitsí'powata-oaawa -> kitsí'powatawaawa
#test á-Io'kaa-wa -> áyo'kaawa
#test yaatóó-t -> aatóót
#test waaníí-t -> aaníít
#test w-óko'si -> óko'si
#test á-yo'kaa-o'pa -> áyo'kao'pa
#test imitáá-iksi -> imitáíksi
#test á-yo'kaa-yi-aawa -> áyo'kaayaawa
#test á-ihpiyi-o'pa -> áíhpiyo'pa
#test á-okstaki-yi-aawa -> áókstakiiyaawa
#test á-okstaki-yi-aawa -> áókstakiyaawa
#test á-okska'si-o'pa -> áókska'so'pa
#test nit-Ioyi -> nitsoyi
#test otokska'si-hsi -> otokska'ssi
#test otá'po'taki-hsi -> otá'po'takssi
#test pii-hsini -> pissini
#test áak-yaatoowa -> áakaatoowa
#test nit-waanii -> nitaanii
#test kikáta'-waaniihpa -> kikáta'waaniihpa
#test áíhpiyi-yináyi -> áíhpiiyináyi
#test áíhpiyi-yináyi -> áíhpiyiyináyi
#test áókska'si-hpinnaan -> áókska'sspinnaan
#test nit-it-itsiniki -> nitsitsitsiniki
#test á'-omai'taki-wa -> áó'mai'takiwa
#test káta'-ookaawaatsi -> kátaookaawaatsi
#test káta'-ottakiwaatsi -> kátaoottakiwaatsi
#test á'-isttohkohpiy'ssi -> áíisttohkohpiy'ssi
#test á'-o'tooyiniki -> áó'tooyiniki
#test káta'-ohto'toowa -> kátao'ohto'toowa
#test káta'-ohto'toowa -> kátaohto'toowa
#test nit-ssksinoawa -> nitssksinoawa
#test á-okska'siwa -> áókska'siwa
#test atsikí-istsi -> atsikíístsi
#test kakkóó-iksi -> kakkóíksi
#test nit-ihpiyi -> nitsspiyi
#test sa-oht-yi -> saohtsi
#test nit-yo'kaa -> nitso'kaa
#test nit-áak-yo'kaa -> nitáakso'kaa
#test nit-áak-ooyi -> nitáaksoyi
#test nit-ooyi -> nitsoyi
#test ooyi -> iiyi
#test nit-yooht-wa -> nitoohtowa
#test nit-yooht-o-aa -> nitsííyoohtoaa
#test nit-yáapi -> nitsaapi
#test nit-yáapi -> nitsíaapi
#
#
# Reusable Regexes
################################################################################
define phonemes [ p | t | k | m | n | s | w | y | h | "'" | a | i | o | á | í | ó ] ;
define vowels [ a | i | o | á | í | ó ] ;
define accentedVowels [ á | í | ó ] ;
define consonants [ p | t | k | m | n | s | w | y ] ;
define obstruents [ p | t | k | m | n | s ] ;
define stops [ p | t | k | m | n ] ;
define plosives [ p | t | k ] ;
define glides [ w | y ] ;


# Rules
################################################################################

# 1.    C1-C2     ->     C2C2
# Gemination
define gemination [
    [ plosives "-" -> p || _ p ] .o.
    [ plosives "-" -> t || _ t ] .o.
    [ plosives "-" -> k || _ k ] ] ;

# 2.    It    ->    Ist
# s-Insertion (assumes that "breaking I" is a phoneme)
define sInsertion [..] -> s || I _ t ;

# 3.a.    C-s    ->    Css
# s-Connection A
define sConnectionA "-" -> s || stops _ s ;

# 3.b.    V(')-s    ->    V(')-is
# s-Connection B
# condition: where 's' is not part of a suffix
# present implementation: rule is optional
define sConnectionB [..] (->) i || vowels ("'") "-" _ s ;

# 4.    o-a    ->    aa
# o-Replacement
# note: for some speakers the o is deleted
# condition: where 'a' is not part of a suffix
# present implementation: rule is optional
define oReplacement [
    [ o (->) [ a | 0 ] , ó (->) [ á | 0 ] || _ "-" a ] .o.
    [ [ o | ó ] (->) [ á | 0 ] || _ "-" á ] ] ;

# 5.    w-i(i)    ->    o
# Coalescence
define coalescence [
    w "-" i (i) -> o ,
    w "-" í (í) -> ó ||
    _ [ p | t | k | m | n | s | w | y | h | "'" ] ] ;

# 6.    k-I    ->    ksi
# Breaking
define breaking "-" -> s || k _ I ;

# 7.    I -> i
# Neutralization
define neutralization I -> i ;

# 8.a.    V-iV    ->    VyV
# Desyllabification A
define desyllabificationA "-" i -> y || vowels _ [ a | á | o | ó ] ;

# 8.b.    V-oV    ->    VwV
# Desyllabification B
define desyllabificationB "-" o -> w || vowels _ [ a | á | i | í | ó ] ;

# 9.    #G    ->    0
# Semivowel Drop
define semivowelDrop glides -> 0 || .#. _ ;

# 10.    V1V1-V    ->    V1V
# Vowel Shortening
define vowelShortening [
    [ [ a | á ] -> 0 || [ a | á ] _ "-" vowels ] .o.
    [ [ i | í ] -> 0 || [ i | í ] _ "-" vowels ] .o.
    [ [ o | ó ] -> 0 || [ o | ó ] _ "-" vowels ] ] ;

# 11.    Vyi-{a,o}    ->    Vy{a,o}
# i-Loss 
define iLoss [
    [ [ i | í ] -> 0 || [ a | á | o | ó ] y _ [ a | á | o | ó ] ] .o.
    [ i y [ i | í ] -> i (i) y ,
      í y [ i | í ] -> í (í) y || _ [ a | á | o | ó ] ] ] ;

# 12.    si{a,o}    ->    s{a,o}
# i-Absorption
define iAbsorption [ i | í ] ("-") -> 0 || s _ [ a | á | o | ó ] ;

# 13.    sihs    ->    ss
# ih-Loss
define ihLoss [ i | í ] "-" h -> 0 || s _ s ;

# 14.    ihs    ->    ss
# Presibilation
define presibilation [ i | í ] "-" h -> s || _ s ;

# 15.    CG    ->    C    , where C ne "'"
# Semivowel Loss
define semivowelLoss [
   [ y -> 0 || [ obstruents | I ] "-" _ ] .o. 
   [ w -> 0 || obstruents "-" _ ] ] ;

# 16.    Ciyiy    ->    Ciiy
# y-Reduction (optional)
define yReduction y (->) 0 || [ obstruents | "'" ] [ i | í ] _ [ i | í ] ("-") y ;

# 17.    sih    ->    ss
# Postsibilation
define postsibilation [ i | í ] ("-") h -> s || s _ ;

# 18.    ti    ->    tsi
# t-Affrication
define tAffrication "-" -> s || t _ [ i | í ] ;

# 19.    V'VC    ->    VV'C
# Glottal Metathesis
define glottalMetathesis [
    "'" "-" a -> "-" a "'" ,
    "'" "-" á -> "-" á "'" ,
    "'" "-" a a -> "-" a a "'" ,
    "'" "-" á á -> "-" á á "'" ,
    "'" "-" i -> "-" i "'" ,
    "'" "-" í -> "-" í "'" ,
    "'" "-" i i -> "-" i i "'" ,
    "'" "-" í í -> "-" í í "'" ,
    "'" "-" o -> "-" o "'" ,
    "'" "-" ó -> "-" ó "'" ,
    "'" "-" o o -> "-" o o "'" ,
    "'" "-" ó ó -> "-" ó ó "'" || vowels _ [ consonants | "'" | h ] ] ;


# 20.    VV1V1'C    ->    VV1V1C
# Glottal Loss
define glottalLoss [
    a a "'" -> a a ,
    á á "'" -> á á ,
    i i "'" -> i i ,
    í í "'" -> í í ,
    o o "'" -> o o ,
    ó ó "'" -> ó ó || vowels ("-") _ consonants ] ;

# 21.    V'(s)CC    ->    VV(s)CC    , where C ne 's'
# Glottal Assimilation
define glottalAssimilation [
    a "'" -> a a || ,
    á "'" -> á á || ,
    i "'" -> i i || ,
    í "'" -> í í || ,
    o "'" -> o o || ,
    ó "'" -> ó ó || _ (s) [ p p | t t | k k | m m | n n ] ] ;

# 22.    ''    ->    '
# Glottal Reduction
define glottalReduction "'" "'" -> "'";

# 23.    V1'h    ->    V1'V1h
# Vowel Epenthesis
# note: In place of this rule, some speakers have the following rule:
# ' -> 0 / _ h
define vowelEpenthesis [ 
    a "'" -> [ a "'" a | a ] ,
    á "'" -> [ á "'" á | á ] ,
    i "'" -> [ i "'" i | i ] ,
    í "'" -> [ í "'" í | í ] ,
    o "'" -> [ o "'" o | o ] ,
    ó "'" -> [ ó "'" ó | ó ] || _ h ] ;

# 24.    sssC    ->    ssC
# sss-Shortening
define sssShortening s -> 0 || _ s s [ stops | glides ] ;

# 25.
# Accent Spread
define accentSpread [
    a -> á ,
    i -> í ,
    o -> ó || accentedVowels "-" _ ] ;

# 26.    - -> 0
# Break-Delete
define breakDelete "-" -> 0 ;

# 27.
# Resolve non-permanent consonants (cf. Frantz 1997: 9)
#  These consonants disappear before vowels, see p. 82 of grammar.
define resolveNonPermanentConsonants [
    [ [ N | M | S ] -> 0 || _ "-" vowels ] .o.
    [ N -> n , M -> m , S -> s || _ [ "-" | .#. ] ] ] ;

# 28.
# Inverse o-Loss ("t-ok-" -> "t-k-")
define oLossINV o -> 0 || t "-" _ k "-" ;

# 29.
# nasal-Loss (might be a more general rule...)
# define nasalLoss [ m | n ] -> 0 || plosives "-" _ ;
#
#
# Phonology (ordered rewrite rules)
################################################################################
define phonology oLossINV .o. 
    resolveNonPermanentConsonants .o. 
    semivowelLoss .o. 
    gemination .o. 
    coalescence .o. 
    sInsertion .o. 
    sConnectionB .o. 
    yReduction .o. 
    breaking .o. 
    oReplacement .o. 
    ihLoss .o. 
    sConnectionA .o. 
    presibilation .o. 
    sssShortening .o. 
    semivowelDrop .o. 
    vowelShortening .o.  
    neutralization .o. 
    tAffrication .o. 
    postsibilation .o. 
    iAbsorption .o.
    desyllabificationB .o. 
    desyllabificationA .o. 
    glottalMetathesis .o. 
    vowelEpenthesis .o. 
    glottalReduction .o. 
    glottalLoss .o. 
    glottalAssimilation .o. 
    accentSpread .o. 
    breakDelete .o. 
    iLoss;

