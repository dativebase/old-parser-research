################################################################################
# The phonological rules of Frantz (1997) as FSTs *with Additional Rules*
################################################################################

# How to understand this file
################################################################################

# - "A -> B || C _ D" means "rewrite A as B only when it occurs between C and D"
# - "(->)" is optional rewrite
# - "define name expression" assigns the FSM/FST generated by "expression" to
#   "name"
# - Special characters (e.g., "-") need to be enclosed in quotes
# - ".o." denotes the composition operation
# - "[..]" denotes the empty symbol in a rewrite rule (using "0" in an insertion
#   rule will result in 1 or more (!) insertions

# Comments
################################################################################

# Some interpretation of the ordered rewrite rules of Frantz (1997) was
# required:
#
# - what to do with the morpheme segmentation symbol "-" in the rules
# - Frantz (1997) provides a partial ordering: some decisions had to be made

# Tests
################################################################################

# A line that begins with "#test" signifies a test.  After "#test" there should
# be a string of characters followed by "->" followed by another string of
# characters.  The first string is the underlying representation and the second
# is the anticipated surface representation.  The OLD can run a phonology script
# against its tests and return a response detailing which tests passed and which
# failed.

#test nit-waanIt-k-wa -> nitaanikka
#test nit-waanIt-aa-wa -> nitaanistaawa
#test nit-siksipawa -> nitssiksipawa
#test nit-ssikópii -> nitsssikópii
#test á-sínaaki-wa -> áísínaakiwa
#test nikáá-ssikópii -> nikáíssikópii
#test káta'-simi-wa -> kátai'simiwa
#test áak-oto-apinnii-wa -> áakotaapinniiwa
#test áak-oto-apinnii-wa -> áakotapinniiwa
#test w-ínni -> ónni
#test w-iihsíssi -> ohsíssi
#test áak-Ipiima -> áaksipiima
#test kitsí'powata-oaawa -> kitsí'powatawaawa
#test á-Io'kaa-wa -> áyo'kaawa
#test yaatóó-t -> aatóót
#test waaníí-t -> aaníít
#test w-óko'si -> óko'si
#test á-yo'kaa-o'pa -> áyo'kao'pa
#test imitáá-iksi -> imitáíksi
#test á-yo'kaa-yi-aawa -> áyo'kaayaawa
#test á-ihpiyi-o'pa -> áíhpiyo'pa
#test á-okstaki-yi-aawa -> áókstakiiyaawa
#test á-okstaki-yi-aawa -> áókstakiyaawa
#test á-okska'si-o'pa -> áókska'so'pa
#test nit-Ioyi -> nitsoyi
#test otokska'si-hsi -> otokska'ssi
#test otá'po'taki-hsi -> otá'po'takssi
#test pii-hsini -> pissini
#test áak-yaatoowa -> áakaatoowa
#test nit-waanii -> nitaanii
#test kikáta'-waaniihpa -> kikáta'waaniihpa
#test áíhpiyi-yináyi -> áíhpiiyináyi
#test áíhpiyi-yináyi -> áíhpiyiyináyi
#test áókska'si-hpinnaan -> áókska'sspinnaan
#test nit-it-itsiniki -> nitsitsitsiniki
#test á'-omai'taki-wa -> áó'mai'takiwa
#test káta'-ookaawaatsi -> kátaookaawaatsi
#test káta'-ottakiwaatsi -> kátaoottakiwaatsi
# NOTE: following test previously has "áíisttohkohpiy" as the correct
#test á'-isttohkohpiy'ssi -> áíísttohkohpiy'ssi
#test á'-o'tooyiniki -> áó'tooyiniki
#test káta'-ohto'toowa -> kátao'ohto'toowa
#test káta'-ohto'toowa -> kátaohto'toowa
#test nit-ssksinoawa -> nitssksinoawa
#test á-okska'siwa -> áókska'siwa
#test atsikí-istsi -> atsikíístsi
#test kakkóó-iksi -> kakkóíksi
#test nit-ihpiyi -> nitsspiyi
#test sa-oht-yi -> saohtsi

#test nit-yo'kaa -> nitso'kaa
#test nit-áak-yo'kaa -> nitáakso'kaa
#test nit-áak-ooyi -> nitáaksoyi
#test nit-ooyi -> nitsoyi
#test ooyi -> iiyi
#test nit-yooht-o-aa -> nitsííyoohtoaa
#test nit-yáapi -> nitsaapi
#test nit-yáapi -> nitsíaapi
#test a'p-á-ssamm-yii -> á'páíssammi
#
#
    
define phonemes [ p | t | k | m | n | s | w | y | h | "'" | a | i | o | á | í | ó ];
define vowels [ a | i | o | á | í | ó ];
define accentedVowels [ á | í | ó ];
define consonants [ p | t | k | m | n | s | w | y ];
define allConsonants [p | t | k | m | n | s | w | y | "'" | h];
define obstruents [ p | t | k | m | n | s ];
define stops [ p | t | k | m | n ];
define plosives [ p | t | k ];
define glides [ w | y ];


# 1.    C1-C2     ->     C2C2
# Gemination
define pGem plosives "-" -> p || _ p;
define tGem plosives "-" -> t || _ t;
define kGem plosives "-" -> k || _ k;
define gemination pGem .o. tGem .o. kGem;

# 2.    It    ->    Ist
# s-Insertion (assumes that "breaking I" is a phoneme)
define sInsertion [..] -> s || I _ t;

# 3.a.    C-s    ->    Css
# s-Connection A
define sConnectionA "-" -> s || stops _ s;

# 3.b.    V(')-s    ->    V(')-is
# s-Connection B
# condition: where 's' is not part of a suffix
# present implementation: rule is optional
define sConnectionB [..] (->) i || vowels ("'") "-" _ s;

# 4.    o-a    ->    aa
# New o-Replacement
# note: for some speakers the o is deleted
# condition: where 'a' is not part of a suffix
# present implementation: rule is optional
define oReplacement o (->) [a | 0] || _ "-" a .o.
                    o o (->) [a a| a] || _ "-" a .o.
                    ó (->) [á | 0] || _ "-" a .o.
                    ó ó (->) [á á| á] || _ "-" a .o.
                    [o | ó] (->) [á | 0] || _ "-" á .o.
                    [o o| ó ó] (->) [á á| á] || _ "-" á;
# 
# Original oReplacement
# define oReplacementA o (->) [a | 0] || _ "-" a;
# define oReplacementB ó (->) [á | 0] || _ "-" a;
# define oReplacementC [o | ó] (->) [á | 0] || _ "-" á;
# define oReplacement oReplacementA .o. oReplacementB .o. oReplacementC;
# 
# 5.    w-i(i)    ->    o
# New Coalescence
define coalescenceA w "-" i (i) -> o || _ ("-") [p|t|k|m|n|s|w|y|h|"'"];
define coalescenceB w "-" í (í) -> ó || _ ("-") [p|t|k|m|n|s|w|y|h|"'"];
define coalescence coalescenceA .o. coalescenceB;

# Original Coalescence
# define coalescenceA w "-" i (i) -> o || _ [p|t|k|m|n|s|w|y|h|"'"];
# define coalescenceB w "-" í (í) -> ó || _ [p|t|k|m|n|s|w|y|h|"'"];
# define coalescence coalescenceA .o. coalescenceB;
#
# 6.    k-I    ->    ksi,    k-i    ->    ksi, optionally
# New Breaking
define breaking "-" -> s || k _ I .o.
                "-" (->) s || k _ [i | í] .o.
                "-" (->) s || p _ I;

# Original breaking
# define breaking "-" -> s || k _ I;
#
# 7.    I -> i
# Neutralization
define neutralization I -> i;

# 8.a.    V-iV    ->    VyV
# New Desyllabification A
# Note the alteration to this rule: now glides form optionally between C and V ...
define desyllabificationA "-" i -> y || vowels _ [a|á|o|ó] .o.
                          "-" i (->) y || obstruents _ ("-") [a|á|o|ó];

# Original desyllabificationA
# define desyllabificationA "-" i -> y || vowels _ [a|á|o|ó];

# 8.b.    CVoV    ->    CVwV
# New Desyllabification B
define desyllabificationB o -> w || consonants ("-") vowels ("-") _ ("-") [a|á|i|í|ó] .o.
                         "-" -> y || consonants o _ [a|á|i|í|ó];
#
# Original desyllabificationB
# define desyllabificationB "-" o -> w || vowels _ [a|á|i|í|ó];
#
# 9.    #G    ->    0
# Semivowel Drop
define semivowelDrop glides -> 0 || .#. _;

# 10.    V1V1-V    ->    V1V
# Vowel Shortening
# define vowelShorteningA [a | á] -> 0 || [a | á] _ "-" vowels;
# define vowelShorteningI [i | í] -> 0 || [i | í] _ "-" vowels;
# define vowelShorteningO [o | ó] -> 0 || [o | ó] _ "-" vowels;
# define vowelShortening vowelShorteningA .o. vowelShorteningI .o. vowelShorteningO;

define vowelShortening [a | á] -> 0 || [a | á] _ "-" vowels .o.
                       [i | í] -> 0 || [i | í] _ "-" vowels .o.
                       [o | ó] -> 0 || [o | ó] _ "-" vowels .o.
                       [a | á] -> 0 || [a | á] ("-") _ ("-") [a | á];

# 11.    Vyi-{a,o}    ->    Vy{a,o}
# i-Loss 
define iLossA [i|í] -> 0 || [a|á|o|ó] y _ [a|á|o|ó];
define iLossB i y [i|í] -> i (i) y || _ [a|á|o|ó];
define iLossC í y [i|í] -> í (í) y || _ [a|á|o|ó];
define iLoss iLossA .o. iLossB .o. iLossC;

# 12.    si{a,o}    ->    s{a,o}
# i-Absorption
define iAbsorption [i|í] ("-") -> 0 || s _ [a|á|o|ó];

# 13.    sihs    ->    ss
# ih-Loss
define ihLoss [i|í] "-" h -> 0 || s _ s;

# 14.    ihs    ->    ss
# Presibilation
define presibilation [i|í] "-" h -> s || _ s;

# 15.    CG    ->    C    , where C ne "'"
# Semivowel Loss
# NOTE: I removed the hyphen delimiter from the context
define semivowelLossY y -> 0 || [obstruents | I] ("-") _;
define semivowelLossW w -> 0 || obstruents ("-") _;
define semivowelLoss semivowelLossY .o. semivowelLossW;

# Original semivowelLoss
# define semivowelLossY y -> 0 || [obstruents | I] "-" _;
# define semivowelLossW w -> 0 || obstruents "-" _;
# define semivowelLoss semivowelLossY .o. semivowelLossW;

#     users inak-atw-aaki                 -> inakataki
# phonology inak-atw-aaki                 -> inakatwaaki
# 
#     users ipon-oo-atw-yii-wa            -> ipóniowatsiiwa
# phonology ipon-oo-atw-yii-wa            -> iponoaatwyiiwa iponoatwyiiwa
# 
#test apw-i -> api
#
#
# 16.    Ciyiy    ->    Ciiy
# y-Reduction (optional)
define yReduction y (->) 0 || [obstruents | "'"] [i|í] _ [i|í] ("-") y;

# 17.    sih    ->    ss
# Postsibilation
define postsibilation [i|í] ("-") h -> s || s _;

# 18.    ti    ->    tsi
# t-Affrication
define tAffrication "-" -> s || t _ [i|í];

# 
# 19.    V'VC    ->    VV'C
# Glottal Metathesis
define glottalMetathesisA "'" "-" a -> "-" a "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisAccA "'" "-" á -> "-" á "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisALong "'" "-" a a -> "-" a a "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisAccALong "'" "-" á á -> "-" á á "'" || vowels _ [consonants|"'"|h];

define glottalMetathesisI "'" "-" i -> "-" i "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisAccI "'" "-" í -> "-" í "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisILong "'" "-" i i -> "-" i i "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisAccILong "'" "-" í í -> "-" í í "'" || vowels _ [consonants|"'"|h];

define glottalMetathesisO "'" "-" o -> "-" o "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisAccO "'" "-" ó -> "-" ó "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisOLong "'" "-" o o -> "-" o o "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisAccOLong "'" "-" ó ó -> "-" ó ó "'" || vowels _ [consonants|"'"|h];

# In order to account for data like the following, we allow /w/ to elide optionally after
# /'-/
#
#     users á-sstsina'-waasai'ni-wa      -> áísstsinaa'sai'niwa
# phonology á-sstsina'-waasai'ni-wa      -> áísstsina'waasai'niwa ásstsina'waasai'niwa
#
# New glottalMetathesis
define glottalMetathesis w (->) 0 || "'" "-" _ .o. 
                         glottalMetathesisA .o.
                         glottalMetathesisAccA .o.
                         glottalMetathesisALong .o.
                         glottalMetathesisAccALong .o.
                         glottalMetathesisI .o.
                         glottalMetathesisAccI .o.
                         glottalMetathesisILong .o.
                         glottalMetathesisAccILong .o.
                         glottalMetathesisO .o.
                         glottalMetathesisAccO .o.
                         glottalMetathesisOLong .o.
                         glottalMetathesisAccOLong;

# Original glottalMetathesis
# define glottalMetathesis glottalMetathesisA .o.
#                          glottalMetathesisAccA .o.
#                          glottalMetathesisALong .o.
#                          glottalMetathesisAccALong .o.
#                          glottalMetathesisI .o.
#                          glottalMetathesisAccI .o.
#                          glottalMetathesisILong .o.
#                          glottalMetathesisAccILong .o.
#                          glottalMetathesisO .o.
#                          glottalMetathesisAccO .o.
#                          glottalMetathesisOLong .o.
#                          glottalMetathesisAccOLong;

# 20.    VV1V1'C    ->    VV1V1C
# Glottal Loss
define glottalLossA a a "'" -> a a || vowels ("-") _ consonants;
define glottalLossAccA á á "'" -> á á || vowels ("-") _ consonants;
define glottalLossI i i "'" -> i i || vowels ("-") _ consonants;
define glottalLossAccI í í "'" -> í í || vowels ("-") _ consonants;
define glottalLossO o o "'" -> o o || vowels ("-") _ consonants;
define glottalLossAccO ó ó "'" -> ó ó || vowels ("-") _ consonants;
define glottalLoss glottalLossA .o. glottalLossAccA .o. glottalLossI .o. 
    glottalLossAccI .o. glottalLossO .o. glottalLossAccO;

# 21.    V'(s)CC    ->    VV(s)CC    , where C ne 's'
# Glottal Assimilation
define glottalAssimilationA a "'" -> a a || _ (s) [p p | t t | k k | m m | n n];
define glottalAssimilationAAcc á "'" -> á á || _ (s) [p p | t t | k k | m m | n n];
define glottalAssimilationI i "'" -> i i || _ (s) [p p | t t | k k | m m | n n];
define glottalAssimilationIAcc í "'" -> í í || _ (s) [p p | t t | k k | m m | n n];
define glottalAssimilationO o "'" -> o o || _ (s) [p p | t t | k k | m m | n n];
define glottalAssimilationOAcc ó "'" -> ó ó || _ (s) [p p | t t | k k | m m | n n];
define glottalAssimilation glottalAssimilationA .o. glottalAssimilationAAcc .o. 
    glottalAssimilationI .o. glottalAssimilationIAcc .o. glottalAssimilationO .o. 
    glottalAssimilationOAcc;

# 22.    ''    ->    '
# Glottal Reduction
define glottalReduction "'" "'" -> "'";

# 23.    V1'h    ->    V1'V1h
# Vowel Epenthesis
# note: In place of this rule, some speakers have the following rule:
# ' -> 0 / _ h
define vowelEpenthesisA a "'" -> [a "'" a | a] || _ h;
define vowelEpenthesisAAcc á "'" -> [á "'" á | á] || _ h;
define vowelEpenthesisI i "'" -> [i "'" i | i] || _ h;
define vowelEpenthesisIAcc í "'" -> [í "'" í | í] || _ h;
define vowelEpenthesisO o "'" -> [o "'" o | o] || _ h;
define vowelEpenthesisOAcc ó "'" -> [ó "'" ó | ó] || _ h;
define vowelEpenthesis vowelEpenthesisA .o. vowelEpenthesisAAcc .o. 
    vowelEpenthesisI .o. vowelEpenthesisIAcc .o. 
    vowelEpenthesisO .o. vowelEpenthesisOAcc;

# 24.    sssC    ->    ssC
# sss-Shortening
define sssShortening s -> 0 || _ s s [stops | glides];

# 25.
# New Accent Spread
define accentSpreadA a -> á || accentedVowels "-" _;
define accentSpreadAA a a -> á á || accentedVowels "-" _;
define accentSpreadI i -> í || accentedVowels "-" _;
define accentSpreadII i i -> í í || accentedVowels "-" _;
define accentSpreadO o -> ó || accentedVowels "-" _;
define accentSpreadOO o o -> ó ó || accentedVowels "-" _;
define accentSpread accentSpreadOO .o.
                    accentSpreadO .o.
                    accentSpreadAA .o.
                    accentSpreadA .o.
                    accentSpreadII .o.
                    accentSpreadI;

# Original accentSpread
# define accentSpreadA a -> á || accentedVowels "-" _;
# define accentSpreadI i -> í || accentedVowels "-" _;
# define accentSpreadO o -> ó || accentedVowels "-" _;
# define accentSpread accentSpreadO .o. accentSpreadA .o. accentSpreadI;
#
# 26.    - -> 0
# Break-Delete
define breakDelete "-" -> 0;

# 27.
# Deletion of Non-permanent Consonants (cf. Frantz 1997: 9)
#  These consonants disappear before vowels, see p. 82 of grammar.
define deleteNonPermanentConsonants N|M|S -> 0 || _ "-" vowels;
define neutralizeNonPermanentConsonantN N -> n || _ ["-" | .#.];
define neutralizeNonPermanentConsonantM M -> m || _ ["-" | .#.];
define neutralizeNonPermanentConsonantS S -> s || _ ["-" | .#.];
define neutralizeNonPermanentConsonants neutralizeNonPermanentConsonantN .o. 
    neutralizeNonPermanentConsonantM .o. neutralizeNonPermanentConsonantS;
define resolveNonPermanentConsonants deleteNonPermanentConsonants .o. 
    neutralizeNonPermanentConsonants;

# 28.
# Inverse o-Loss ("t-ok-" -> "t-k-")
define oLossINV o -> 0 || t "-" _ k "-";

# 29.
# nasal-Loss (might be a more general rule...)
# define nasalLoss m|n -> 0 || plosives "-" _;






# Initial change
# ################################################################################
# 
# Options:
#
# 1. new IC lexical item 
# 2. category-aware phonology
#
#     users nit-yooht-o-aa                -> nitsííyoohtoaa
# phonology nit-yooht-o-aa                -> nitoohtaa nitoohtaaa nitoohtoaa
# 
#     users ssi-ika-aa'si                 -> issííkaasi
# phonology ssi-ika-aa'si                 -> ssiikaaasi
# 
#     users n-asóka'sim-yi               -> nisóóka'simii
# phonology n-asóka'sim-yi               -> nasóka'simi
# 
#     users pookáá-wa'si                -> ipokaawa'si
# phonology pookáá-wa'si                -> pookááwa'si
# 
#     users n-ákáá-ssi-i-hp-wa         -> nikáíssii'pa
# phonology n-ákáá-ssi-i-hp-wa         -> nákáássiihpa nákáíssiihpa
# 
#test nit-i-yooht -> nitsííyooht
#test i-ssi-ika-aa'si -> issííkaasi
#test i-pookáá-wa'si -> ipokaawa'si
#test n-i-ákáá-ssi-i-hp-wa -> nikáíssii'pa
#test ipon-i-oo-atw-yii-wa -> ipóniowatsiiwa
#
#     users ipon-oo-atw-yii-wa            -> ipóniowatsiiwa
# phonology ipon-oo-atw-yii-wa            -> iponoaatwyiiwa iponoatwyiiwa
# 
#     users ot-ksááhko-m-yi             -> otssksááhkoomi
# phonology ot-ksááhko-m-yi             -> okksááhkomi
# 
#     users nit-yáak-iksi-i-hp-wa        -> nitsíyaaksiksi'pa
# phonology nit-yáak-iksi-i-hp-wa        -> nitáakiksiihpa
# 
#     users nit-yooht-o-aa               -> nitsííyoohtoaa
# phonology nit-yooht-o-aa               -> nitoohtoaa, nitoohtaa, nitoohtaa
#
#     users ssi-ika-aa'si                 -> issííkaasi
# phonology ssi-ika-aa'si                 -> ssiikaaasi
#
#     users áka-piikáni-wa              -> ákaipiikániwa
# phonology áka-piikáni-wa              -> ákapiikániwa
#
#     users iit-kaanaisskiinaa-attsi-yii-wa-áyi-> iitsikánaiskiinawattsiwáy
# phonology iit-kaanaisskiinaa-attsi-yii-wa-áyi-> iikkaanaisskiinaattsiyiiwaáyi
#
#     users ooyi                         -> iiyi
# phonology ooyi                         -> ooy'i, ooyi, ooy, oo
#
#     users nit-yo'kaa                   -> nitso'kaa
# phonology nit-yo'kaa                   -> nito'kaa
#
# - is the following IC or breaking resulting from a phoneme, i.e., /Iooyi/, cf.
# Frantz (1971):
# 
#     users áak-ohk-ooyi                 -> aakohksooyi
# phonology áak-ohk-ooyi                 -> áakohkooyi
# 
# - is IC implicated in the following?:  (Interesting)
# 
#     users iksow-áwákaasii             -> ksisówáwakaasi
# phonology iksow-áwákaasii             -> iksowáwákaasii
# 
# 
# Inalienably possessed /m-/ prefix -- remove it from lexical items?
# ################################################################################
# 
# NOTE: this will require lexical changes or phonologies that can see categorial and
# semantic information (if that will even be enough...)
#
#     users ot-áaksisaa-moápssp-yi      -> otáksisawa'psspii
# phonology ot-áaksisaa-moápssp-yi      -> otáaksisaamoápsspi
# 
# - I incorrectly use /ot-/ as the third person nominal possessor prefix.  I should
#   use /w-/.
# 
#     users ot-mo'tokáán                -> o'tokán
# phonology ot-mo'tokáán                -> otmo'tokáán
# 
# 
# Mistranscription/shortening of geminates, long vowels and accented vowels
# ################################################################################
# 
# NOTE: see "misspell" rule below.
#
# Singletons and geminates can be shortened or lengthened (ugh!)
define misspell p (p) (->) p (p) .o.
                t (t) (->) t (t) .o.
                k (k) (->) k (k) .o.
                m (m) (->) m (m) .o.
                n (n) (->) n (n) .o.
                s (s) (->) s (s) .o.
                [a | á] ([a | á]) (->) [a | á] ([a | á]) .o.
                [i | í] ([i | í]) (->) [i | í] ([i | í]) .o.
                [o | ó] ([o | ó]) (->) [o | ó] ([o | ó]);

#test ot-áaksisaa -> otáksisaa
#test n-asóka'sim-yi -> nisóóka'simii
#test miistsis -> mistsíís
#

#     users n-asóka'sim-yi               -> nisóóka'simii
# phonology n-asóka'sim-yi               -> nasóka'simi
# 
#     users miistsis                      -> mistsíís
# phonology miistsis                      -> miistsis
#
#     users ot-áaksisaa-moápssp-yi      -> otáksisawa'psspii
# phonology ot-áaksisaa-moápssp-yi      -> otáaksisaamoápsspi
# 
#     users á-ssi-ist-st-aaki            -> áíssiiststákii
# phonology á-ssi-ist-st-aaki            -> áíssiistsstaaki ássiistsstaaki
# 
#     users ni'tókskaa-m                 -> ni'tóókskam
# phonology ni'tókskaa-m                 -> ni'tókskaam
# 
#     users ot-ohkiimi-n                  -> otoohkííman
# phonology ot-ohkiimi-n                  -> otohkiimin
# 
#     users nit-á-okstoo-hp-wa           -> nitaoksstoopa
# phonology nit-á-okstoo-hp-wa           -> nitáókstoohpa
# 
#     users ot-ksááhko-m-yi             -> otssksááhkoomi
# phonology ot-ksááhko-m-yi             -> okksááhkomi
# 
#     users nit-yáak-iksi-i-hp-wa        -> nitsíyaaksiksi'pa
# phonology nit-yáak-iksi-i-hp-wa        -> nitáakiksiihpa
# 
#     users ot-áaksisaa                 -> otáksisaa
# phonology ot-áaksisaa                 -> otáaksisaa
# 
#
# Glide insertion
# ################################################################################
# 
# NOTE: see alteration to desyllabificationB above.
#
#test nit-iik-sopo-a'p -> nitsiikssopoya'p
#test ipon-oo-at -> iponowat
#test nit-ipo-i'po-yi -> nitsipoyi'poyi
#
# /o-a/ -> [oya]
# 
#     users nit-iik-sopo-a'p-I            -> nitsííksopoya'ps
# phonology nit-iik-sopo-a'p-I            -> nitsiikssopa'pi nitsiikssopaa'pi nitsiikssopoa'pi
# 
# /o-a/ -> [owa]
# 
#     users ipon-oo-atw-yii-wa            -> ipóniowatsiiwa
# phonology ipon-oo-atw-yii-wa            -> iponoaatwyiiwa iponoatwyiiwa
# 
# /o-i/ -> [oyi]
# 
#     users nit-ipo-i'po-yi               -> nitsípoyi'poyi
# phonology nit-ipo-i'po-yi               -> nitsipoi'poyi
# 
# 
# /p/ breaking
# ################################################################################
# 
# NOTE: see the alteration to the "breaking" rule.
# NOTE 2: it may be that the lexical item /-I/ "be" should actually be /-si/.
#
#test nit-iik-sopo-a'p-I            -> nitsiikssopa'psi
#
#     users nit-iik-sopo-a'p-I            -> nitsííksopoya'ps
# phonology nit-iik-sopo-a'p-I            -> nitsiikssopa'pi nitsiikssopaa'pi nitsiikssopoa'pi
# 
# 
# Resonant glottalization (before voiceless nuclei)
# ################################################################################
# 
define resonantGlottalization [..] (->) "'" || [m | n | w | y] _ [a | i | o | á | ó | í] [h | .#.];

#test kit-á-simi                   -> kitáísim'i
#test nit-i'poyi-hpinnaan -> nitsi'poy'ihpinnaan
#
# Does the syllable onset condition this?
# 
#     users nit-i'poyi-hpinnaan           -> nitsí'poy'ihpinnaan nitsíí'poyihpinnan
# phonology nit-i'poyi-hpinnaan           -> nitsi'poyihpinnaan
# 
# Note the interaction with word-final vowel devoicing:
# 
#     users kit-á-simi                   -> kitáísim'i
# phonology kit-á-simi                   -> kitáísimi kitásimi
# 
# 
# ***Default prominence***
# ################################################################################
# 
# NOTE: see the "defaultProminence" rule below.
# NOTE #2: this is a very crude and unsatisfactory "solution" to the issue...
# NOTE: I am *not* including this in the phonology at this point since the even
# cruder "misspell" rule should take care of this ...
#
define defaultProminenceA a (->) á .o. i (->) í .o. o (->) ó;
#
# ***Default syllable two prominence***
# ################################################################################
#
# Optionally accents every vowel in syllable two.
# This is an attempt to model the very vague generalization that Blackfoot words
# tend to have prominence in syllable two.  One problem with this approach is that
# it does not "move" prominence from neighbouring syllables which is what, impressionistically,
# appears to be happening.
#
# Another idea: add prominence to syllable three if syll 2 is devoiced
#     users ot-ohkiimi-n                  -> otoohkííman
# phonology ot-ohkiimi-n                  -> otohkiimin
# 
#
define onsetOptionalSyllable allConsonants* vowels+;
define syllable allConsonants+ vowels+;
define preContext .#. onsetOptionalSyllable allConsonants+;
define postContext [allConsonants+ | .#.];
define defaultSyllableTwoProminence a a (->) á á || preContext _ postContext .o.
                                    á a (->) á á || preContext _ postContext .o.
                                    a á (->) á á || preContext _ postContext .o.
                                    a (->) á || preContext _ postContext .o.
                                    i i (->) í í || preContext _ postContext .o.
                                    i í (->) í í || preContext _ postContext .o.
                                    í i (->) í í || preContext _ postContext .o.
                                    i (->) í || preContext _ postContext .o.
                                    o o (->) ó ó || preContext _ postContext .o.
                                    o ó (->) ó ó || preContext _ postContext .o.
                                    ó o (->) ó ó || preContext _ postContext .o.
                                    o (->) ó || preContext _ postContext .o.
                                    a i i (->) á í í || preContext _ postContext .o.
                                    a i (->) á í || preContext _ postContext .o.
                                    a o o (->) á ó ó || preContext _ postContext .o.
                                    a o (->) á ó || preContext _ postContext .o.
                                    o i i (->) ó í í || preContext _ postContext .o.
                                    o i (->) ó í || preContext _ postContext;

#
#     users nit-a'-ipiitsiy-áak-okska'si -> nitáí'piitsiyáakokska'si
# phonology nit-a'-ipiitsiy-áak-okska'si -> nitai'piitsiyáakokska'si
# 
#     users iik-it-ssto-yi                -> iikítsstoyi
# phonology iik-it-ssto-yi                -> iikitsstoyi
# 
#     users nit-inaani                    -> nitíínaan nitsinaan'i nitsináán nitsíínaan nitsíínaan'i nitsínaan nitsínaani
# phonology nit-inaani                    -> nitsinaani
# 
#
#
#test iik-it-ssto-yi -> iikítsstoyi
#test nit-a'-ipiitsiy-áak-okska'si -> nitáí'piitsiyáakokska'si
#test nit-inaani -> nitsínaani
#test kit-inaani -> kitsinaan'i
#test ot-inaani -> otsínaan
#test nit-á-okstoo-hp-wa -> nitáókstoohp
#test ot-ihkiitaa-n-istsi -> otsskíítaanistsi
#
#
#
#     users waatsímma'pssi-wa            -> áátsimma'pssiwa
# phonology waatsímma'pssi-wa            -> aatsímma'pssiwa
# 
#     users aakíí                       -> aakii
# phonology aakíí                       -> aakíí
# 
#     users miistsis                      -> mistsíís
# phonology miistsis                      -> miistsis
# 
#     users á-ssi-ist-st-aaki            -> áíssiiststákii
# phonology á-ssi-ist-st-aaki            -> áíssiistsstaaki ássiistsstaaki
# 
#     users imitáá-wa                   -> imítaawa
# phonology imitáá-wa                   -> imitááwa
# 
#     users ot-ohkiimi-n                  -> otoohkííman
# phonology ot-ohkiimi-n                  -> otohkiimin
# 
#     users nit-á-okstoo-hp-wa           -> nitaoksstoopa
# phonology nit-á-okstoo-hp-wa           -> nitáókstoohpa
# 
#     users ot-ihkiitaa-n-istsi           -> otsskíítaanistsi
# phonology ot-ihkiitaa-n-istsi           -> otsskiitaanistsi
# 
# - a lexical problem in the following form too (?):
# 
#     users nit-ohkoitapiiyi-hsin-yi      -> nitohkóítapiiwahsini
# phonology nit-ohkoitapiiyi-hsin-yi      -> nitohkoitapiiyssini
# 
#     users iik-ikkak-yi                  -> iiksíkkaki
# phonology iik-ikkak-yi                  -> iikikkaki
# 
#     users it-iinohk-opii-wa             -> itsíínohkópiiwa
# phonology it-iinohk-opii-wa             -> itsiinohkopiiwa
# 
# 
# Word-final vowel drop
# ################################################################################
# 
# NOTE: see "wordFinalVowelDrop" rule below.
#
define wordFinalVowelDrop [a | i | o | á | í | ó] (->) 0 || [p | t | k | m | n | s | y | w ] _ .#. .o.
                          [y | w] (->) 0 || _ .#.;
#
#     users kit-á-simi                   -> kitáísim'i
# phonology kit-á-simi                   -> kitáísimi kitásimi
# 
#     users nit-inaani                    -> nitíínaan nitsinaan'i nitsináán nitsíínaan nitsíínaan'i nitsínaan nitsínaani
# phonology nit-inaani                    -> nitsinaani
# 
# It can include the glide onset as well:
# 
#     users ayak-o'too-yi-aawa            -> ayaako'toyaa
# phonology ayak-o'too-yi-aawa            -> ayako'tooyaawa
# 
# Or not ...
# 
#     users omó'tsi-aa-yi-aawa           -> áomótsaiaw
# phonology omó'tsi-aa-yi-aawa           -> omó'tsaayaawa
# 
# 
# Possessed nominals and "initial change"
# ################################################################################
# 
# NOTE: I am going to avoid dealing with this phenomenon until I have more data on 
# it.  Two ways of potentially handling it:
# 
# 1. /imitáá/ is actually /wimitáá/ (or something like that)
# 2. /nit/ and /kit/ on nominals are actually /nitw/ and /kitw/
# 
#     users nit-imitáá-m-wa             -> nitómitaama
# phonology nit-imitáá-m-wa             -> nitsimitááma
# 
# 
# Post-plosive glide elision
# ################################################################################
# 
# NOTE: the rule "semivowelLoss" should be taking care of the examples below.  For
# some reason it isn't ...
#
#test inak-atw-aaki -> inakataaki
#
#     users inak-atw-aaki                 -> inakataki
# phonology inak-atw-aaki                 -> inakatwaaki
# 
#     users ipon-oo-atw-yii-wa            -> ipóniowatsiiwa
# phonology ipon-oo-atw-yii-wa            -> iponoaatwyiiwa iponoatwyiiwa
# 
# 
# Velar pre-aspiration
# ################################################################################
# 
# NOTE: see "velarPreaspiration" rule below.
#
#     users n-ko's                        -> noohko's
# phonology n-ko's                        -> nko's
# 
define velarPreaspiration "-" -> o h || [m | n] _ k;
# 
#test n-ko's -> nohko's
# 
#
# /oo/ "go" allomorphy ? ...
# ################################################################################
# 
# NOTE: I need more information on the behaviour of this morpheme before I go 
# positing phonological transformations for what might be a lexical issue.
#
#     users ipo-oo                        -> ipowá
# phonology ipo-oo                        -> ipooo
# 
# 
# Cross-glide glottal metathesis
# ################################################################################
# 
# NOTE: see the alteration to the glottalMetathesis rule.
#
#test á-sstsina'-waasai'ni-wa -> áísstsinaa'sai'niwa
#
#     users á-sstsina'-waasai'ni-wa      -> áísstsinaa'sai'niwa
# phonology á-sstsina'-waasai'ni-wa      -> áísstsina'waasai'niwa ásstsina'waasai'niwa
# 
# 
# Breaking extended
# ################################################################################
# 
# NOTE: see the alteration to the existing rule "breaking".
#
# I think that Armoskaite found that this was constrained by morphological domain ...
#
#test sik-ika-ikoan -> siksikaikoan
#
#     users sik-ika-ikoan                 -> siksikáíkoan
# phonology sik-ika-ikoan                 -> sikikaikoan
# 
#     users iik-i'pii                     -> iiksi'pii
# phonology iik-i'pii                     -> iiki'pii
# 
#     users iik-ómahk-inaattsi           -> iikómahksinattsi
# phonology iik-ómahk-inaattsi           -> iikómahkinaattsi
# 
#     users iik-ikkak-yi                  -> iiksíkkaki
# phonology iik-ikkak-yi                  -> iikikkaki
# 
#     users á-iik-isamo                  -> áiiksisamó
# phonology á-iik-isamo                  -> áíikisamo
# 
# 
# Complex coda resolution: insert /i/ (?)
# ################################################################################
# 
# NOTE: there might be some more morphology here.  I'm gonna hold off on this one.
#
#     users ipak-mo'tsís-m               -> ipakóttsisim
# phonology ipak-mo'tsís-m               -> ipakmo'tsísm
# 
# 
# Glottal stoppification
# ################################################################################
# 
# NOTE: see "glottalStoppification" rule below.
#
define glottalStoppification h (->) "'" || "-" _;

#test ti-hp -> ti'p
#
#     users káta'-itstsii-hp             -> kátai'tstsi'p
# phonology káta'-itstsii-hp             -> kátai'tstsiihp
# 
#     users m-ááhk-aanist-opam-oo-atw-i-hsi-> mahkanistopamato'si
# phonology m-ááhk-aanist-opam-oo-atw-i-hsi-> mááhkaanistopamoaatwssi mááhkaanistopamoatwssi
# 
#     users n-ákáá-ssi-i-hp-wa         -> nikáíssii'pa
# phonology n-ákáá-ssi-i-hp-wa         -> nákáássiihpa nákáíssiihpa
# 
#     users nit-yáak-iksi-i-hp-wa        -> nitsíyaaksiksi'pa
# phonology nit-yáak-iksi-i-hp-wa        -> nitáakiksiihpa
# 
# 
# Coalescence needs alteration
# ################################################################################
# 
# NOTE: see alteration to "coalescence" rule.
#
#test atw-i-hsi-> atohsi
#
#     users m-ááhk-aanist-opam-oo-atw-i-hsi-> mahkanistopamato'si
# phonology m-ááhk-aanist-opam-oo-atw-i-hsi-> mááhkaanistopamoaatwssi mááhkaanistopamoatwssi
# 
# 
# /oo-a/ -> [aa] ???
# ################################################################################
# 
# NOTE: see alteration to "oReplacement" rule.
#
#test m-ááhk-aanist-opam-oo-at -> mááhkaanistopamaat
#
#     users m-ááhk-aanist-opam-oo-atw-i-hsi-> mahkanistopamato'si
# phonology m-ááhk-aanist-opam-oo-atw-i-hsi-> mááhkaanistopamoaatwssi mááhkaanistopamoatwssi
# 
# 
# desyllabificationA extended: /iV/ -> [yV] optionally (spelling variation)
# ################################################################################
# 
# NOTE: see alteration to the rule "desyllabificationA".
#
#test nit-á-sstss-i-aaki -> nitásstssyaaki
#
#     users nit-á-sstss-i-aaki           -> nitásstssyaaki
# phonology nit-á-sstss-i-aaki           -> nitáísstssiaaki nitásstssiaaki
# 
#     users omó'tsi-aa-yi-aawa           -> áomótsaiaw
# phonology omó'tsi-aa-yi-aawa           -> omó'tsaayaawa
# 
# 
# ??? /s/-elision after INV-/o/-loss (?)
# ################################################################################
# 
# What is going on here?  For one thing, it involves what I call in my phonology
# "inverse /o/ loss".  But why is the /s/ disappearing too?  Could this /is/ sequence
# really be the /I/ that "pre-breaks" a following /t/, as in Frantz (1971) where
# /aanist/ is transcribed as /aanIt/?...
# 
# NOTE: decided to hold off on writing a rule for this until more data come to light.
#
#     users nit-inn-aapiksist-ok-wa       -> nitsínnáápiksikka
# phonology nit-inn-aapiksist-ok-wa       -> nitsinnaapiksiskka
# 
# 
# Glottal stop elision (?)
# ################################################################################
# 
# NOTE: I am unsure whether there are enough data to motivate the positing of such
# a rule, so I will hold off on this one.  This is probably just researcher mistranscription.
#
#     users omó'tsi-aa-yi-aawa           -> áomótsaiaw
# phonology omó'tsi-aa-yi-aawa           -> omó'tsaayaawa
# 
#
# Accent spread not spreading far enough?
# ################################################################################
# 
# NOTE: see alteration to the "accentSpread" rule.
# NOTE: what does a sequence like [aii] mean?  Should it be reduced to [ai]?
#
#test á-iik-isamo -> áííksisamo
#
#     users á-iik-isamo                  -> áiiksisamó
# phonology á-iik-isamo                  -> áíikisamo
# 


# defaultProminenceA
#
define phonology oLossINV .o. 
    velarPreaspiration .o.
    resolveNonPermanentConsonants .o. 
    glottalStoppification .o.
    coalescence .o. 
    semivowelLoss .o. 
    gemination .o. 
#    coalescence .o. 
    sInsertion .o. 
    sConnectionB .o. 
    yReduction .o. 
    breaking .o. 
    oReplacement .o. 
    ihLoss .o. 
    sConnectionA .o. 
    presibilation .o. 
    sssShortening .o. 
    semivowelDrop .o. 
    neutralization .o. 
    tAffrication .o. 
    postsibilation .o. 
    iAbsorption .o.
    desyllabificationB .o. 
    desyllabificationA .o. 
    glottalMetathesis .o. 
    vowelShortening .o.  
    vowelEpenthesis .o. 
    glottalReduction .o. 
    glottalLoss .o. 
    glottalAssimilation .o. 
    accentSpread .o. 
    breakDelete .o. 
    iLoss .o.
    wordFinalVowelDrop .o.
    resonantGlottalization .o.
    misspell ;


#test nit-i-yáapi nitsaapi
#test nit-i-yáapi nitsíaapi
#test atw-i-hsi atohsi

